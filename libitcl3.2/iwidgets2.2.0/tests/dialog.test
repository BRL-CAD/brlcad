# This file is a Tcl script to test out [incr Widgets] Dialog class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#   4.x - Other tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

package require -exact Iwidgets 2.2

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Dialog-1.$c {Dialog construction} {
    iwidgets::Dialog .dlg

    listbox [.dlg childsite].lb -relief sunken
    pack [.dlg childsite].lb -fill both -expand yes

    .dlg activate
} {}

incr c

#
# Option tests which are successful.
#
test Dialog-2.$o {configuration option} {
    llength [.dlg configure]
} {15}

incr o

foreach test {
    {-background #d9d9d9 #d9d9d9} 
    {-buttonboxpadx 10 10} 
    {-buttonboxpady 10 10} 
    {-buttonboxpos n n} 
    {-buttonboxpos e e} 
    {-buttonboxpos w w} 
    {-buttonboxpos s s} 
    {-cursor gumby gumby} 
    {-modality global global} 
    {-modality application application} 
    {-modality none none} 
    {-padx 15 15} 
    {-pady 15 15} 
    {-separator off off} 
    {-thickness 5 5} 
    {-separator on on} 
    {-title Dialog Dialog}} {
	set option [lindex $test 0]
	test Dialog-2.$o "configuration options, $option" {
	    .dlg configure $option [lindex $test 1]
	    lindex [.dlg configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.dlg childsite} {.dlg.shellchildsite.dschildsite}}
    {{.dlg hide Help} {}}
    {{.dlg hide Cancel} {}}
    {{.dlg default Apply} {}}
    {{.dlg buttonconfigure Help -state disabled} {}}
    {{.dlg show Cancel} {}}
    {{.dlg deactivate} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Dialog-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Dialog-1.$c {Dialog destruction} {
    destroy .dlg
    update 
} {}

incr c

test Dialog-1.$c {Dialog construction} {
    iwidgets::Dialog .dlg
    update 
} {}

incr c

test Dialog-1.$c {Dialog destruction} {
    destroy .dlg
    update 
} {}

incr c

test Dialog-1.$c {Dialog destruction} {
    iwidgets::dialog .dlg
    destroy .dlg
    update 
} {}


::tcltest::cleanupTests
exit
