# This file is a Tcl script to test out [incr Widgets] Scrolledtext class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Scrolledtext-1.$c {Scrolledtext construction} {
    iwidgets::Scrolledtext .st
    pack .st -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

#
# Option tests which are successful.
#
test Scrolledtext-2.$o {configuration option} {
    llength [.st configure]
} {49}

incr o

foreach test {
    {-background #d9d9d9 #d9d9d9} 
    {-borderwidth 3 3} 
    {-cursor gumby gumby} 
    {-exportselection 0 0} 
    {-exportselection 1 1} 
    {-foreground Black Black} 
    {-height 120 120} 
    {-width 500 500}
    {-insertbackground Black Black} 
    {-insertborderwidth 1 1} 
    {-insertofftime 200 200} 
    {-insertontime 500 500} 
    {-insertwidth 3 3} 
    {-labelmargin 5 5} 
    {-labeltext Label Label}
    {-labelpos nw nw}
    {-labelpos ne ne}
    {-labelpos en en}
    {-labelpos e e}
    {-labelpos es es}
    {-labelpos se se}
    {-labelpos s s}
    {-labelpos sw sw}
    {-labelpos wn wn}
    {-labelpos w w}
    {-labelpos ws ws}
    {-labelpos n n}
    {-relief raised raised} 
    {-relief sunken sunken} 
    {-vscrollmode none none} 
    {-vscrollmode static static} 
    {-vscrollmode dynamic dynamic} 
    {-hscrollmode none none} 
    {-hscrollmode static static} 
    {-hscrollmode dynamic dynamic} 
    {-sbwidth 20 20} 
    {-scrollmargin 5 5} 
    {-selectborderwidth 2 2} 
    {-state disabled disabled} 
    {-state normal normal} 
    {-sticky w w}
    {-sticky nw nw}
    {-sticky n n}
    {-sticky ne ne}
    {-sticky e e}
    {-sticky se se}
    {-sticky s s}
    {-sticky sw sw}
    {-sticky news news}
    {-textbackground GhostWhite GhostWhite} 
    {-visibleitems 72x40 72x40}
    {-height 0 0}
    {-width 0 0}
    {-wrap char char}
    {-wrap none none}} {
	set option [lindex $test 0]
	test Scrolledtext-2.$o "configuration options, $option" {
	    .st configure $option [lindex $test 1]
	    lindex [.st configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Option tests which fail and produce errors.
#
foreach test {
  {-visibleitems bogus {bad visibleitems option "bogus": should be widthxheight}}
  {-hscrollmode bogus {bad hscrollmode option "bogus": should be static, dynamic, or none}}
  {-vscrollmode bogus {bad vscrollmode option "bogus": should be static, dynamic, or none}}} {
	set option [lindex $test 0]
        test Scrolledtext-2.$o "configuration options, $option" {
	    list [catch {.st configure $option [lindex $test 1]} msg] $msg
	} [list 1 [lindex $test 2]]
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.st import ./scrolledtext.test} {}}
    {{.st export /tmp/scrolledtext.test} {}}
    {{.st clear} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Scrolledtext-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Scrolledtext-1.$c {Scrolledtext destruction} {
    destroy .st
    update 
} {}

incr c

test Scrolledtext-1.$c {Scrolledtext construction} {
    iwidgets::scrolledtext .st -hscrollmode dynamic -labeltext "Label" \
	    -labelpos n -labelmargin 5
    pack .st -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

test Scrolledtext-1.$c {Scrolledtext destruction} {
    destroy .st
    update 
} {}

incr c

test Scrolledtext-1.$c {Scrolledtext destruction} {
    iwidgets::scrolledtext .st
    pack .st
    destroy .st
    update 
} {}

::tcltest::cleanupTests
exit
