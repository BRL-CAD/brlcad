# This file is a Tcl script to test out [incr Widgets] Extbutton class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

if {$::tk_version < 8.4} {
  puts "*****"
  puts " --> Skipping extbutton tests.  This iwidget only available with\
    Tk 8.4 or greater."
  puts "*****"
  ::tcltest::cleanupTests
  exit
}

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Extbutton-1.$c {Extbutton construction} {
    iwidgets::Extbutton .eb 
    pack .eb -padx 10 -pady 10 -expand yes
    update 
} {}

incr c

#
# Option tests which are successful.
#
test Extbutton-2.$o {configuration option} {
    llength [.eb configure]
} {21}

incr o

foreach test {
    {-activebackground #ececec #ececec}
    {-activeforeground Black Black}
    {-background #d9d9d9 #d9d9d9}
    {-bd 2 2}
    {-bitmap info info}
    {-bitmapforeground blue blue}
    {-command {} {}}
    {-cursor {} {}}
    {-defaultring 1 1}
    {-defaultringpad 4 4}
    {-disabledforeground #a3a3a3 #a3a3a3}
    {-font {Helvetica -12 bold} {Helvetica -12 bold}}
    {-foreground Black Black}
    {-justify center center}
    {-relief raised raised}
    {-ringbackground #d9d9d9 #d9d9d9}
    {-state normal normal}
    {-text {Hello World} {Hello World}}} {
	set option [lindex $test 0]
	test Extbutton-2.$o "configuration options, $option" {
	    .eb configure $option [lindex $test 1]
	    lindex [.eb configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Option tests which fail and produce errors.
#
foreach test {
  {-image bogus {image "bogus" doesn't exist}}
  {-bitmap bogus {bitmap "bogus" not defined}}} {
	set option [lindex $test 0]
        test Extbutton-2.$o "configuration options, $option" {
	    list [catch {.eb configure $option [lindex $test 1]} msg] $msg
	} [list 1 [lindex $test 2]]
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.eb invoke} {}}
    {{.eb flash} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Extbutton-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Extbutton-1.$c {Extbutton destruction} {
    destroy .eb
    update 
} {}

incr c

test Extbutton-1.$c {Extbutton construction} {
    iwidgets::extbutton .eb 
    pack .eb -padx 10 -pady 10
    update 
} {}

incr c

test Extbutton-1.$c {Extbutton destruction} {
    destroy .eb
    update 
} {}

::tcltest::cleanupTests
exit
