#  M U L T I C O N F I G _ P A T H _ R E A D . C M A K E . I N
# BRL-CAD
#
# Copyright (c) 2012-2014 United States Government as represented by
# the U.S. Army Research Laboratory.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following
# disclaimer in the documentation and/or other materials provided
# with the distribution.
#
# 3. The name of the author may not be used to endorse or promote
# products derived from this software without specific prior written
# permission.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS
# OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
# GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
# NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
###
file(GLOB ALL_FILES "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/*")
foreach(foundfile ${ALL_FILES})
  if(NOT "${foundfile}" MATCHES "rule$")
    get_filename_component(BUILD_TYPE "${foundfile}" NAME)
  endif(NOT "${foundfile}" MATCHES "rule$")
endforeach(foundfile ${ALL_FILES})

# For Debug and Release builds, obey the standard BRL-CAD
# install directory naming convention
if("${BUILD_TYPE}" STREQUAL "Debug")
  set(BUILD_TYPE_NEW "dev")
endif("${BUILD_TYPE}" STREQUAL "Debug")
if("${BUILD_TYPE}" STREQUAL "Release")
  set(BUILD_TYPE_NEW "rel")
endif("${BUILD_TYPE}" STREQUAL "Release")
if(NOT BUILD_TYPE_NEW)
  set(BUILD_TYPE_NEW "${BUILD_TYPE}")
endif(NOT BUILD_TYPE_NEW)

# The installation directory is compiled in, so to that extent at least a build time configuration
# change that results in a different installation path requires a re-compile.  Write to a temp file
# to allow the build target to avoid changing the PATH file unless there is an actual path change -
# this minimizes recompilation based on PATH to actual installation path changes, rather than
# just configuration changes - if CMAKE_INSTALL_PREFIX is not defined based on the build configuration,
# PATH is constant.
string(REPLACE "----BUILD_TYPE----" "${BUILD_TYPE_NEW}" CMAKE_INSTALL_PREFIX_CFG "@CMAKE_INSTALL_PREFIX_CFG@")
file(WRITE "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/PATH.tmp" "\"${CMAKE_INSTALL_PREFIX_CFG}\"\n")

# We have no way to tell the build tool at build time what string we want
# it to use for the installation path - it only knows its build configuration
# type.  Hence, we update the cmake_install.cmake files to let them know
# directly.  Only do this if we need to - i.e. we're using a string other
# than the build type string itself in the install path.
#
# In the event of multiple configuration switches we need to avoid matching failures
# due to previous substitutions, so the first time we do this stash the original
# contents in cmake_install.cmake.orig file.
string(REPLACE "----BUILD_TYPE----" "\${BUILD_TYPE}" CMAKE_INSTALL_PREFIX_CFG_ORIG "@CMAKE_INSTALL_PREFIX_CFG@")
if(NOT "${BUILD_TYPE_NEW}" STREQUAL "${BUILD_TYPE}")
  file(GLOB_RECURSE ALL_CMAKE_INSTALL_FILES_ORIG "cmake_install.cmake.orig")
  file(GLOB_RECURSE ALL_CMAKE_INSTALL_FILES "cmake_install.cmake")
  foreach(cmake_install_file ${ALL_CMAKE_INSTALL_FILES})
    if(ALL_CMAKE_INSTALL_FILES_ORIG)
      file(READ "${cmake_install_file}.orig" OLD_INSTALL_CONTENTS)
    else(ALL_CMAKE_INSTALL_FILES_ORIG)
      file(READ "${cmake_install_file}" OLD_INSTALL_CONTENTS)
      file(WRITE "${cmake_install_file}.orig" "\n${OLD_INSTALL_CONTENTS}")
    endif(ALL_CMAKE_INSTALL_FILES_ORIG)
    string(REPLACE "${CMAKE_INSTALL_PREFIX_CFG_ORIG}" "${CMAKE_INSTALL_PREFIX_CFG}" INSTALL_CONTENTS "${OLD_INSTALL_CONTENTS}")
    string(REPLACE "@CMAKE_BINARY_DIR@/@CMAKE_CFG_INTDIR@" "@CMAKE_BINARY_DIR@/${BUILD_TYPE}" INSTALL_CONTENTS "${INSTALL_CONTENTS}")
    file(WRITE "${cmake_install_file}" "${INSTALL_CONTENTS}")
  endforeach(cmake_install_file ${ALL_CMAKE_INSTALL_FILES})
endif(NOT "${BUILD_TYPE_NEW}" STREQUAL "${BUILD_TYPE}")

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8
