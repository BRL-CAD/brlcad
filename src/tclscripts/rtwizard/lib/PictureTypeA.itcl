#               P I C T U R E T Y P E A . I T C L
# BRL-CAD
#
# Copyright (c) 2004-2014 United States Government as represented by
# the U.S. Army Research Laboratory.
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public License
# version 2.1 as published by the Free Software Foundation.
#
# This library is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this file; see the file named COPYING for more
# information.
#
###
#
# This class defines the process necessary to build a BRL-CAD
# picture of Type "A". This is a simple full-color rendering, e.g.
# rt with the Phong shader.
#

#
# Required packages
#
package require Itcl
package require Itk
package require Iwidgets

namespace eval RtWizard {

    package require PictureTypeBase 1.0
    package provide PictureTypeA 1.0

    #
    # Required packages
    #

    # None!

    #
    # The PictureTypeA class supports a simple Phong-shaded image.
    # Note that all public methods are specified in the abstract
    # base class.
    #
    ::itcl::class PictureTypeA {
	inherit ::RtWizard::PictureTypeBase

	public {
	    constructor {args} {
		PictureTypeBase::constructor $args
	    } {}

	    #
	    # Methods that implement abstract base class methods.
	    # Unfortunately, I have to redeclare them here.
	    #
	    method activate {}
	    method deactivate {}
	    method rtimage {w h}
	    method preview {}
	    method fullSize {}
	}

	private {
	    variable fcp
	}
    }

    #--------------------#
    #   Public Methods   #
    #--------------------#
    #
    #
    # activate - called when this picture type is selected.
    #
    itcl::body PictureTypeA::activate { } {
	#
	# Open a new steps menu
	#
	$::wizardInstance openSteps

	#
	# Update the introductory text
	#
	$::introInstance configure -greeting "$introText"

	#
	# Activate the pages that are necessary for this picture
	# type.
	#
	set fcp [$::wizardInstance enable fullColor]
	[$fcp component useMe] select
    }

    #
    # deactivate - called when this picture type is deselected.
    #
    itcl::body PictureTypeA::deactivate { } {
	#
	# Deactivate the pages.
	#
	$::wizardInstance disable fullColor

	#
	# Close the current steps menu
	#
	$::wizardInstance closeSteps
    }

    #
    # rtimage - generates a picture.
    #
    itcl::body PictureTypeA::rtimage {w h} {

	#
	# get a framebuffer
	#
	set fb [$::fbp getFrameBuffer $w $h]
	set fbarray [split $fb " "]
	if { [llength $fbarray] > 1 } {
	   set fb [lindex $fbarray 0]
	   set fbpid [lindex $fbarray 1]
	   set filename [lindex $fbarray 2]
	}

	#
	# Get the view info
	#
	set viewPage [$::exp getViewPage]
	set viewInfo [$viewPage getEyeModel]
	set view_info [regsub -all ";" $viewInfo ""]
	set vdata [split $view_info "\n"]
	set viewsize [lindex [lindex $vdata 0] 1]
	set orientation [lrange [lindex $vdata 1] 1 end]
	set eye_pt [lrange [lindex $vdata 2] 1 end]

	#
	# Get the background color
	#
	set objPage $fcp
	set bgHex [$objPage getBackgroundColor]
	set bg [hexToRGB $bgHex]

	#
	# Get the object list, the file is global
	#
	set color_objects [$objPage getObjects]

	# Get the perspective angle
	#
	set perspective [$viewPage getPerspective]

        # populate a dictionary to pass to rtimage
        set rtimage_dict [dict create \
        _dbfile $::RtWizard::wizard_state(dbFile) \
        _port $fb \
        _w $w \
        _n $h \
        _viewsize $viewsize \
        _orientation $orientation \
        _eye_pt $eye_pt \
        _perspective $perspective \
        _bgcolor $bg \
        _color_objects $color_objects ]

	::cadwidgets::rtimage $rtimage_dict

	# If we're outputting a file, write it out and close down
	# the in-memory fbserv
	if {[info exists filename]} {
	   if {[string length $filename] > 0 } {
	       set output_generated 0
	       if {[file extension $filename] == ".png"} {
		  exec [file join [bu_brlcad_root bin] fb-png] -w $w -n $h -F $fb $filename
		  set output_generated 1
	       }
	       if {!$output_generated} {
		  exec [file join [bu_brlcad_root bin] fb-pix] -w $w -n $h -F $fb $filename
		  set output_generated 1
	       }
	       if {$::tcl_platform(platform) == "windows"} {
		   set kill_cmd [auto_execok taskkill]
	       } else {
		   set kill_cmd [auto_execok kill]
	       }

	       if {$kill_cmd != ""} {
		   exec $kill_cmd $fbpid
	       }
	   }
	}
    }


    #
    # preview - generates a 1/4 size preview image, limited to on-screen
    #
    itcl::body PictureTypeA::preview { } {
	#
	# get height and width
	#
	set w [$::fbp getWidth]
	set w2 [ expr $w / 2 ]

	set h [$::fbp getHeight]
	set h2 [ expr $h / 2 ]

	rtimage $w2 $h2
    }

    #
    # fullSize - generates a fullSize picture.
    #
    itcl::body PictureTypeA::fullSize { } {
	#
	# get height and width
	#
	set w [$::fbp getWidth]
	set h [$::fbp getHeight]

	rtimage $w $h
    }


}; # end namespace


# Local Variables:
# mode: Tcl
# tab-width: 8
# c-basic-offset: 4
# tcl-indent-level: 4
# indent-tabs-mode: t
# End:
# ex: shiftwidth=4 tabstop=8
