dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during [incr Tcl]
dnl installation to configure the system for the local environment.

# RCS: $Id$

AC_INIT([iwidgets], [4.0.2])

TEA_INIT([3.2])

AC_CONFIG_AUX_DIR(tclconfig)

AC_CONFIG_SRCDIR([generic/buttonbox.itk])

ITCL_VERSION=3.2
AC_SUBST(ITCL_VERSION)

#--------------------------------------------------------------------
#   See if there was a command-line option for where Tcl or Tk is;  if
#   not, search for Tcl and/or Tk.
#--------------------------------------------------------------------

TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG

TEA_PATH_TKCONFIG
TEA_LOAD_TKCONFIG

AC_PROG_INSTALL

#--------------------------------------------------------------------
# See if --with-itcl was specified to give the [incr Tcl] and [incr Tk]
# source code location.  If not, then we'll assume that the Iwidgets
# package is being installed directly beneath the toplevel Itcl directory.
#--------------------------------------------------------------------

AC_ARG_WITH(itcl, [  --with-itcl=DIR         use Itcl 3.0 binaries from DIR],
        itcl_search=$withval, itcl_search=`cd ${srcdir}/..; pwd`)

ITCL_LIB_DIR=""
if test -r $itcl_search/itcl/library/itcl.tcl \
	-a -r $itcl_search/itk/library/itk.tcl; then
    ITCL_LIB_DIR=$itcl_search/itcl/library
    ITCL_SRC_DIR=$itcl_search/itcl
    ITK_LIB_DIR=$itcl_search/itk/library
    ITK_SRC_DIR=$itcl_search/itk
fi

if test -z "$ITCL_LIB_DIR"; then
    AC_MSG_ERROR([Can't find Itcl source.  Use --with-itcl to specify the the toplevel [incr Tcl] directory on your system.  This directory should contain itcl/library/itcl.tcl, itk/library/itk.tcl.])
fi

#--------------------------------------------------------------------
# Handle the --prefix=... option by defaulting to what Tcl gave.
# Must be called after TEA_LOAD_TCLCONFIG and before $prefix is used.
#--------------------------------------------------------------------

TEA_PREFIX

#--------------------------------------------------------------------
#   Needed for running the test suite
#--------------------------------------------------------------------

TEA_PROG_TCLSH
TEA_PROG_WISH

#--------------------------------------------------------------------
#   Fill in template files with the proper info.
#--------------------------------------------------------------------
AC_SUBST(TCL_SRC_DIR)
AC_SUBST(TK_SRC_DIR)
AC_SUBST(ITCL_LIB_DIR)
AC_SUBST(ITCL_SRC_DIR)
AC_SUBST(ITK_LIB_DIR)
AC_SUBST(ITK_SRC_DIR)
AC_SUBST(IWIDGETS_SRC_DIR)

AC_OUTPUT([Makefile iwidgets.tcl pkgIndex.tcl])
