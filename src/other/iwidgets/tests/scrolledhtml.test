# This file is a Tcl script to test out [incr Widgets] Scrolledhtml class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id

package require tcltest
namespace import -force ::tcltest::*

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Scrolledhtml-1.$c {Scrolledhtml construction} {
    iwidgets::Scrolledhtml .sh
    pack .sh -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

#
# Option tests which are successful.
#
test Scrolledhtml-2.$o {configuration option} {
    llength [.sh configure]
} {60}

incr o

foreach test {
    {-background #d9d9d9 #d9d9d9} 
    {-borderwidth 3 3} 
    {-cursor gumby gumby} 
    {-exportselection 0 0} 
    {-exportselection 1 1} 
    {-foreground Black Black} 
    {-height 120 120} 
    {-width 500 500}
    {-insertbackground Black Black} 
    {-insertborderwidth 1 1} 
    {-insertofftime 200 200} 
    {-insertontime 500 500} 
    {-insertwidth 3 3} 
    {-labelmargin 5 5} 
    {-labeltext Label Label}
    {-labelpos n n} 
    {-labelpos ne ne} 
    {-labelpos e e} 
    {-labelpos se se} 
    {-labelpos s s} 
    {-labelpos sw sw} 
    {-labelpos w w} 
    {-labelpos nw nw} 
    {-relief raised raised} 
    {-relief sunken sunken} 
    {-vscrollmode none none} 
    {-vscrollmode static static} 
    {-vscrollmode dynamic dynamic} 
    {-hscrollmode none none} 
    {-hscrollmode static static} 
    {-hscrollmode dynamic dynamic} 
    {-sbwidth 20 20} 
    {-scrollmargin 5 5} 
    {-selectborderwidth 2 2} 
    {-state disabled disabled} 
    {-state normal normal} 
    {-sticky w w}
    {-sticky nw nw}
    {-sticky n n}
    {-sticky ne ne}
    {-sticky e e}
    {-sticky se se}
    {-sticky s s}
    {-sticky sw sw}
    {-sticky news news}
    {-textbackground GhostWhite GhostWhite} 
    {-visibleitems 72x40 72x40}
    {-height 0 0}
    {-width 0 0}
    {-wrap char char}
    {-wrap none none}
    {-feedback puts puts}
    {-feedback {} {}}
    {-linkcommand puts puts}
    {-linkcommand {} {}}
    {-unknownimage {} {}}
    {-link blue blue}
    {-linkhighlight red red}
    {-fontname times times}
    {-fixedfont courier courier}
    {-fontsize medium medium}
    {-update 1 1}} {
	set option [lindex $test 0]
	test Scrolledhtml-2.$o "configuration options, $option" {
	    .sh configure $option [lindex $test 1]
	    lindex [.sh configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Option tests which fail and produce errors.
#
foreach test {
  {-visibleitems bogus {bad visibleitems option "bogus": should be widthxheight}}
  {-hscrollmode bogus {bad hscrollmode option "bogus": should be static, dynamic, or none}}
  {-vscrollmode bogus {bad vscrollmode option "bogus": should be static, dynamic, or none}}
  {-fontname bogus {Invalid font name "bogus". Must be one of  helvetica courier times symbol}}
  {-fontsize bogus {bad fontsize option "bogus": should be small, medium, large, or huge}}
  {-fixedfont bogus {Invalid font name "bogus". Must be one of  helvetica courier times symbol}}} {
	set option [lindex $test 0]
        test Scrolledhtml-2.$o "configuration options, $option" {
	    list [catch {.sh configure $option [lindex $test 1]} msg] $msg
	} [list 1 [lindex $test 2]]
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.sh import ./scrolledhtml.test} {}}
    {{.sh pwd} {.}}
    {{.sh export /tmp/scrolledhtml.test} {}}
    {{.sh render {
        <title>Test</title>
  <body bgcolor="#000000" link="#40a0f0" text="#00d0d0" background="notyet.gif">
        <h1>This is a test</h1>
        <strong>of the emergency broadcast system.</strong>
        <em>This is only a test.</em><p>
        <tt>If this were a real html document</tt><br>
        <pre>it would be displayed</pre><br>
        <center>for more than an instant</center>
        <blockquote>Four score and seven years ago</blockquote>
        <code>int main { printf(\&quotHello World!\&quot);}</code>
        <dl><dt>write only memory 
        <dd>the obvious antonym to read-only memory</dl>
        <ol>reasons this is a short list:<li>it only has one entry</ol>
        <ul>reasons this is a short list:<li>it only has one entry</ul>
        <a name="linktome" href="./test.html#linktome">
       <img src="invalid.filename" alt="No_image_here" width=100 height=100>
        </a>
        </body>
                  }} {}}
    {{.sh title} {Test}}
    {{.sh clear} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Scrolledhtml-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of construction/destruction tests
#
test Scrolledhtml-1.$c {Scrolledhtml destruction} {
    destroy .sh
    update 
} {}

incr c

test Scrolledhtml-1.$c {Scrolledhtml construction} {
    iwidgets::scrolledhtml .sh -hscrollmode dynamic -labeltext "Label" \
	    -labelpos n -labelmargin 5
    pack .sh -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

test Scrolledhtml-1.$c {Scrolledhtml destruction} {
    destroy .sh
    update 
} {}

incr c

test Scrolledhtml-1.$c {Scrolledhtml destruction} {
    iwidgets::scrolledhtml .sh
    pack .sh
    destroy .sh
    update 
} {}

::tcltest::cleanupTests
exit
