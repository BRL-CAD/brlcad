# This file is a Tcl script to test out [incr Widgets] Radiobox class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Radiobox-1.$c {Radiobox construction} {
    iwidgets::Radiobox .rb -labeltext "Radiobox"
    pack .rb -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

#
# Option tests which are successful.
#
test Radiobox-2.$o {configuration option} {
    llength [.rb configure]
} {19}

incr o

test Radiobox-1.$c {Radiobox add method} {
    set ::radiocommand 0
    .rb configure -command {set ::radiocommand 1}
    .rb add foo -text Foo
    .rb add bar -text Bar
} {bar}
update 

incr m

test Radiobox-1.$m {Radiobox add test} {
    #
    # This is the 2nd part of the previous test
    # It was added (along with the modifications to the previous
    # test) to test for a bug that was fixed in version 1.8 of the
    # radiobox. -- Marty Backe 26 February 2002
    #
    set ::radiocommand
} {0}

incr m

foreach test {
    {-background #d9d9d9 #d9d9d9}
    {-borderwidth 4 4} 
    {-borderwidth 2 2} 
    {-cursor gumby gumby} 
    {-foreground Green Green} 
    {-foreground Black Black} 
    {-labelmargin 5 5} 
    {-labelpos w w} 
    {-labelpos nw nw} 
    {-labelpos n n} 
    {-labelpos ne ne} 
    {-labelpos e e} 
    {-labelpos se se} 
    {-labelpos s s} 
    {-labelpos sw sw} 
    {-labeltext Label Label} 
    {-relief raised raised} 
    {-relief sunken sunken}} {
	set option [lindex $test 0]
	test Radiobox-2.$o "configuration options, $option" {
	    .rb configure $option [lindex $test 1]
	    lindex [.rb configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.rb insert bar zoo -text Zoo} zoo}
    {{.rb index z*} 1}
    {{.rb select foo} {}}
    {{.rb get} foo}
    {{.rb delete end} {}}
    {{.rb deselect foo} {}}
    {{.rb get} {}}
    {{.rb index end} 1}
    {{.rb flash 1} {}}
    {{.rb buttonconfigure foo -text FOO} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Radiobox-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Radiobox-1.$c {Radiobox destruction} {
    destroy .rb
    update 
} {}

incr c

test Radiobox-1.$c {Radiobox construction} {
    iwidgets::radiobox .rb
    pack .rb -padx 10 -pady 10
    update 
} {}

incr c

test Radiobox-1.$c {Radiobox destruction} {
    destroy .rb
    update 
} {}

incr c

test Radiobox-1.$c {Radiobox destruction} {
    iwidgets::radiobox .rb
    pack .rb
    destroy .rb
    update 
} {}

::tcltest::cleanupTests
exit
