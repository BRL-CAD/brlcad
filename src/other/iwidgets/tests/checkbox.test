# This file is a Tcl script to test out [incr Widgets] Checkbox class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Checkbox-1.$c {Checkbox construction} {
    iwidgets::Checkbox .cb -labeltext "Styles"
    pack .cb -padx 10 -pady 10 -fill both -expand yes
    update 
} {}

incr c

#
# Option tests which are successful.
#
test Checkbox-2.$o {configuration option} {
    llength [.cb configure]
} {16}

incr o

test Checkbox-1.$c {Checkbox add method} {
    .cb add foo -text "Foo Bar"
    .cb add bar -text "Bar Foo"
    update 
} {}

incr m

foreach test {
    {-background #d9d9d9 #d9d9d9}
    {-borderwidth 4 4} 
    {-borderwidth 2 2} 
    {-cursor gumby gumby} 
    {-foreground Green Green} 
    {-foreground Black Black} 
    {-labelmargin 5 5} 
    {-labelpos w w} 
    {-labelpos n n} 
    {-labelpos ne ne} 
    {-labelpos e e} 
    {-labelpos se se} 
    {-labelpos s s} 
    {-labelpos sw sw} 
    {-labeltext Label Label} 
    {-labeltext Styles Styles} 
    {-labelpos nw nw} 
    {-relief raised raised} 
    {-relief sunken sunken}} {
	set option [lindex $test 0]
	test Checkbox-2.$o "configuration options, $option" {
	    .cb configure $option [lindex $test 1]
	    lindex [.cb configure $option] 4
	} [lindex $test 2]
	update 
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.cb delete foo} {}}
    {{.cb delete bar} {}}
    {{.cb add bold -text Bold} bold}
    {{.cb insert bold italic -text Italic} italic}
    {{.cb add underline -text Underline} underline}
    {{.cb insert underline strikethrough -text "Strike Through"} strikethrough}
    {{.cb index b*} 1}
    {{.cb select bold} {}}
    {{.cb get} bold}
    {{.cb get bold} 1}
    {{.cb get italic} 0}
    {{.cb delete end} {}}
    {{.cb deselect bold} {}}
    {{.cb get} {}}
    {{.cb index end} 2}
    {{.cb flash 1} {}}
    {{.cb buttonconfigure bold -text BOLD} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Checkbox-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update 
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Checkbox-1.$c {Checkbox destruction} {
    destroy .cb
    update 
} {}

incr c

test Checkbox-1.$c {Checkbox construction} {
    iwidgets::checkbox .cb
    pack .cb -padx 10 -pady 10
    update 
} {}

incr c

test Checkbox-1.$c {Checkbox destruction} {
    destroy .cb
    update 
} {}

incr c

test Checkbox-1.$c {Checkbox destruction} {
    iwidgets::checkbox .cb
    pack .cb
    destroy .cb
    update 
} {}

::tcltest::cleanupTests
exit
