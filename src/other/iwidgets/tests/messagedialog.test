# This file is a Tcl script to test out [incr Widgets] Messagedialog class.
# It is organized in the standard fashion for Tcl tests with the following
# notation for test case labels:
#
#   1.x - Construction/Destruction tests
#   2.x - Configuration option tests
#   3.x - Method tests
#   4.x - Other tests
#
# Copyright (c) 1995 DSC Technologies Corporation
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id$

package require tcltest
namespace import -force ::tcltest::*

if [catch {package require Iwidgets 4.0}] {
  # Let's try modifying the auto_path.  Note that the IWIDGETS_LIBRARY
  # env var is initialized in the Makefile when doing a 'make test'.
  # If sourcing this file independently, this variable must be set manually.
  if ![info exists env(IWIDGETS_LIBRARY)] {
    error "Unable to locate Iwidgets package.  Set your IWIDGETS_LIBRARY\
      environment\nvariable to the directory that contains iwidgets.tcl"
  }
  lappend auto_path $env(IWIDGETS_LIBRARY)
  package require Iwidgets 4.0
}

if {[string compare test [info procs test]] == 1} {
    source defs
}

wm geometry . {}
raise .

set c 1
set o 1
set m 1

#
# Initial construction test
#
test Messagedialog-1.$c {Messagedialog construction} {
    iwidgets::Messagedialog .md -text "Are you sure ?" -bitmap questhead
    image create bitmap flagup -file $tk_library/demos/images/flagup.bmp
    .md activate
} {}

incr c

#
# Option tests which are successful.
#
test Messagedialog-2.$o {configuration option} {
    llength [.md configure]
} {26}

incr o

foreach test {
    {-background #d9d9d9 #d9d9d9} 
    {-buttonboxpos n n} 
    {-buttonboxpadx 10 10} 
    {-buttonboxpos e e} 
    {-buttonboxpos w w} 
    {-buttonboxpady 10 10} 
    {-buttonboxpos s s} 
    {-imagepos n n} 
    {-bitmap warning warning} 
    {-imagepos e e} 
    {-imagepos s s} 
    {-cursor gumby gumby} 
    {-image flagup flagup}
    {-imagepos w w} 
    {-font -Adobe-Helvetica-Bold-R-Normal--*-120-*-*-*-*-*-* -Adobe-Helvetica-Bold-R-Normal--*-120-*-*-*-*-*-*} 
    {-foreground Black Black}
    {-modality global global} 
    {-modality application application} 
    {-modality none none} 
    {-padx 15 15} 
    {-pady 15 15} 
    {-textpadx 15 15} 
    {-textpady 15 15} 
    {-separator off off} 
    {-thickness 4 4} 
    {-separator on on} 
    {-image {} {}}
    {-title Messagedialog Messagedialog}} {
	set option [lindex $test 0]
	test Messagedialog-2.$o "configuration options, $option" {
	    .md configure $option [lindex $test 1]
	    lindex [.md configure $option] 4
	} [lindex $test 2]
	update
	incr o
}

#
# Option tests which fail and produce errors.
#
foreach test {
  {-imagepos bogus {bad imagepos option "bogus": should be n, e, s, or w}}} {
	set option [lindex $test 0]
        test Messagedialog-2.$o "configuration options, $option" {
	    list [catch {.md configure $option [lindex $test 1]} msg] $msg
	} [list 1 [lindex $test 2]]
	incr o
}

#
# Method tests which are successful.
#
foreach test {
    {{.md childsite} {.md.shellchildsite.dschildsite}}
    {{.md hide Help} {}}
    {{.md hide Cancel} {}}
    {{.md default Apply} {}}
    {{.md show Cancel} {}}
    {{.md deactivate} {}}} {
	set method [lindex [lindex $test 0] 1]
	test Messagedialog-3.$m "object methods, $method" {
	    list [catch {eval [lindex $test 0]} msg] $msg
	} [list 0 [lindex $test 1]]
	update
	incr m
}

#
# Conclusion of constrcution/destruction tests
#
test Messagedialog-1.$c {Messagedialog destruction} {
    destroy .md
    update
} {}

incr c

test Messagedialog-1.$c {Messagedialog construction} {
    iwidgets::Messagedialog .md
    update
} {}

incr c

test Messagedialog-1.$c {Messagedialog destruction} {
    destroy .md
    image delete flagup
    update
} {}

incr c

test Messagedialog-1.$c {Messagedialog construction} {
    iwidgets::messagedialog .md
    destroy .md
    update
} {}

::tcltest::cleanupTests
exit
