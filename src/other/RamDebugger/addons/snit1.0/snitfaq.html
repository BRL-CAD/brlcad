<! -- -*- tcl -*- doctools manpage
   -->
<html><head>
<title>snitfaq - Snit's Not Incr Tcl, OO system </title>
</head>
<! -- Generated from file './modules/snit/snitfaq.man' by tcllib/doctools with format 'html'
   -->
<! -- Copyright &copy; 2003-2005, by William H. Duquette
   -->
<! -- CVS: $Id$ snitfaq.n
   -->

<body>
<h1> snitfaq(n) 1.0 snit &quot;Snit's Not Incr Tcl, OO system&quot;</h1>
<h2><a name="name">NAME</a></h2>
<p>
<p> snitfaq - Snit Frequently Asked Questions



<h2><a name="table_of_contents">TABLE OF CONTENTS</a></h2>
<p>&nbsp;&nbsp;&nbsp;&nbsp;<a href="#table_of_contents">TABLE OF CONTENTS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#description">DESCRIPTION</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#overview">OVERVIEW</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_this_document?">What is this document?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_snit?">What is Snit?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_version_of_tcl_does_snit_require?">What version of Tcl does Snit require?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#where_can_i_download_snit?">Where can I download Snit?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_are_snit's_goals?">What are Snit's goals?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_snit_different_from_other_oo_frameworks?">How is Snit different from other OO frameworks?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_can_i_do_with_snit?">What can I do with Snit?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#objects">OBJECTS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_object?">What is an object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_abstract_data_type?">What is an abstract data type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_kinds_of_abstract_data_types_does_snit_provide?">What kinds of abstract data types does Snit provide?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_snit::type?">What is a snit::type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_snit::widget?">What is a snit::widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_snit::widgetadaptor?">What is a snit::widgetadaptor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_create_an_instance_of_a_snit::type?">How do I create an instance of a snit::type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_refer_to_an_object_indirectly?">How do I refer to an object indirectly?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_generate_the_object_name_automatically?">How can I generate the object name automatically?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_types_be_renamed?">Can types be renamed?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_objects_be_renamed?">Can objects be renamed?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_destroy_a_snit_object?">How do I destroy a Snit object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#instance_methods">INSTANCE METHODS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_instance_method?">What is an instance method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_an_instance_method?">How do I define an instance method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_a_client_call_an_instance_method?">How does a client call an instance method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_an_instance_method_call_another_instance_method?">How does an instance method call another instance method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_instance_method_names?">Are there any limitations on instance method names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_make_an_instance_method_private?">How do I make an instance method private?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_instance_method_arguments?">Are there any limitations on instance method arguments?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_implicit_arguments_are_passed_to_each_instance_method?">What implicit arguments are passed to each instance method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_$type?">What is $type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_$self?">What is $self?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_$selfns?">What is $selfns?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_$win?">What is $win?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_pass_an_instance_method_as_a_callback?">How do I pass an instance method as a callback?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_delegate_instance_methods_to_a_component?">How do I delegate instance methods to a component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#instance_variables">INSTANCE VARIABLES</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_instance_variable?">What is an instance variable?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_a_scalar_instance_variable_defined?">How is a scalar instance variable defined?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_an_array_instance_variable_defined?">How is an array instance variable defined?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_instance_variable_names?">Are there any limitations on instance variable names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#do_i_need_to_declare_my_instance_variables_in_my_methods?">Do I need to declare my instance variables in my methods?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_pass_an_instance_variable's_name_to_another_object?">How do I pass an instance variable's name to another object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_make_an_instance_variable_public?">How do I make an instance variable public?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#options">OPTIONS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_option?">What is an option?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_an_option?">How do I define an option?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_a_client_set_options_at_object_creation?">How can a client set options at object creation?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_a_client_retrieve_an_option's_value?">How can a client retrieve an option's value?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_a_client_set_options_after_object_creation?">How can a client set options after object creation?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_should_an_instance_method_access_an_option_value?">How should an instance method access an option value?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_make_an_option_read-only?">How can I make an option read-only?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_catch_accesses_to_an_option's_value?">How can I catch accesses to an option's value?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_-cgetmethod?">What is a -cgetmethod?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_catch_changes_to_an_option's_value?">How can I catch changes to an option's value?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_-configuremethod?">What is a -configuremethod?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_validate_an_option's_value?">How can I validate an option's value?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_-validatemethod?">What is a -validatemethod?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#type_variables">TYPE VARIABLES</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_type_variable?">What is a type variable?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_a_scalar_type_variable_defined?">How is a scalar type variable defined?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_an_array-valued_type_variable_defined?">How is an array-valued type variable defined?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_type_variable_names?">Are there any limitations on type variable names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#do_i_need_to_declare_my_type_variables_in_my_methods?">Do I need to declare my type variables in my methods?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_pass_a_type_variable's_name_to_another_object?">How do I pass a type variable's name to another object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_make_a_type_variable_public?">How do I make a type variable public?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#type_methods">TYPE METHODS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_type_method?">What is a type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_type_method?">How do I define a type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_a_client_call_a_type_method?">How does a client call a type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_type_method_names?">Are there any limitations on type method names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_make_a_type_method_private?">How do I make a type method private?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_type_method_arguments?">Are there any limitations on type method arguments?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_an_instance_or_type_method_call_a_type_method?">How does an instance or type method call a type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_pass_a_type_method_as_a_callback?">How do I pass a type method as a callback?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#procs">PROCS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_proc?">What is a proc?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_proc?">How do I define a proc?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_proc_names?">Are there any limitations on proc names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_a_method_call_a_proc?">How does a method call a proc?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_pass_a_proc_to_another_object_as_a_callback?">How can I pass a proc to another object as a callback?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#type_constructors">TYPE CONSTRUCTORS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_type_constructor?">What is a type constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_type_constructor?">How do I define a type constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#constructors">CONSTRUCTORS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_constructor?">What is a constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_constructor?">How do I define a constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_does_the_default_constructor_do?">What does the default constructor do?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_choose_a_different_set_of_arguments_for_the_constructor?">Can I choose a different set of arguments for the constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_constructor_arguments?">Are there any limitations on constructor arguments?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#is_there_anything_special_about_writing_the_constructor?">Is there anything special about writing the constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#destructors">DESTRUCTORS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_destructor?">What is a destructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_destructor?">How do I define a destructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_destructor_arguments?">Are there any limitations on destructor arguments?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_implicit_arguments_are_passed_to_the_destructor?">What implicit arguments are passed to the destructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#must_components_be_destroyed_explicitly?">Must components be destroyed explicitly?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#is_there_any_special_about_writing_a_destructor?">Is there any special about writing a destructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#components">COMPONENTS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_component?">What is a component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_declare_a_component?">How do I declare a component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_is_a_component_named?">How is a component named?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_component_names?">Are there any limitations on component names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_owned_component?">What is an owned component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_does_the_install_command_do?">What does the install command do?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#must_owned_components_be_created_in_the_constructor?">Must owned components be created in the constructor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_component_object_names?">Are there any limitations on component object names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#must_i_destroy_the_components_i_own?">Must I destroy the components I own?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_expose_a_component's_object_command_as_part_of_my_interface?">Can I expose a component's object command as part of my interface?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_expose_a_component's_object_command?">How do I expose a component's object command?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#type_components">TYPE COMPONENTS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_type_component?">What is a type component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_declare_a_type_component?">How do I declare a type component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_install_a_type_component?">How do I install a type component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_any_limitations_on_type_component_names?">Are there any limitations on type component names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#delegation">DELEGATION</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_delegation?">What is delegation?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_a_method_to_a_component_object?">How can I delegate a method to a component object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_delegate_to_a_method_with_a_different_name?">Can I delegate to a method with a different name?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_delegate_to_a_method_with_additional_arguments?">Can I delegate to a method with additional arguments?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_delegate_a_method_to_something_other_than_an_object?">Can I delegate a method to something other than an object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_a_method_to_a_type_component_object?">How can I delegate a method to a type component object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_a_type_method_to_a_type_component_object?">How can I delegate a type method to a type component object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_an_option_to_a_component_object?">How can I delegate an option to a component object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_delegate_to_an_option_with_a_different_name?">Can I delegate to an option with a different name?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_any_unrecognized_method_or_option_to_a_component_object?">How can I delegate any unrecognized method or option to a component object?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_delegate_all_but_certain_methods_or_options_to_a_component?">How can I delegate all but certain methods or options to a component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#widgets">WIDGETS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_snit::widget?">What is a snit::widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_snit::widget?">How do I define a snit::widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_snit::widgets_differ_from_snit::types?">How do snit::widgets differ from snit::types?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_hull_component?">What is a hull component?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_set_the_hull_type_for_a_snit::widget?">How can I set the hull type for a snit::widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_should_i_name_widgets_which_are_components_of_a_snit::widget?">How should I name widgets which are components of a snit::widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#widget_adaptors">WIDGET ADAPTORS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_snit::widgetadaptor?">What is a snit::widgetadaptor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_a_snit::widgetadaptor?">How do I define a snit::widgetadaptor?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_adapt_a_widget_created_elsewhere_in_the_program?">Can I adapt a widget created elsewhere in the program?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#can_i_adapt_another_megawidget?">Can I adapt another megawidget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#the_tk_option_database">THE TK OPTION DATABASE</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_the_tk_option_database?">What is the Tk option database?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#do_snit::types_use_the_tk_option_database?">Do snit::types use the Tk option database?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_my_snit::widget's_widget_class?">What is my snit::widget's widget class?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_my_snit::widgetadaptor's_widget_class?">What is my snit::widgetadaptor's widget class?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_are_option_resource_and_class_names?">What are option resource and class names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_are_the_resource_and_class_names_for_my_megawidget's_options?">What are the resource and class names for my megawidget's options?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_snit_initialize_my_megawidget's_locally-defined_options?">How does Snit initialize my megawidget's locally-defined options?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_snit_initialize_delegated_options?">How does Snit initialize delegated options?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_snit_initialize_options_delegated_to_the_hull?">How does Snit initialize options delegated to the hull?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_does_snit_initialize_options_delegated_to_other_components?">How does Snit initialize options delegated to other components?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_happens_if_i_install_a_non-widget_as_a_component_of_widget?">What happens if I install a non-widget as a component of widget?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#ensemble_commands">ENSEMBLE COMMANDS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_an_ensemble_command?">What is an ensemble command?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_create_an_ensemble_command_using_snit?">How can I create an ensemble command using Snit?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_create_an_ensemble_command_using_an_instance_of_a_snit::type?">How can I create an ensemble command using an instance of a snit::type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_create_an_ensemble_command_using_a_snit::type?">How can I create an ensemble command using a snit::type?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#pragmas">PRAGMAS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_pragma?">What is a pragma?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_set_a_pragma?">How do I set a pragma?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_get_rid_of_the_"info"_type_method?">How can I get rid of the &quot;info&quot; type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_get_rid_of_the_"destroy"_type_method?">How can I get rid of the &quot;destroy&quot; type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_get_rid_of_the_"create"_type_method?">How can I get rid of the &quot;create&quot; type method?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_get_rid_of_type_methods_altogether?">How can I get rid of type methods altogether?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#why_can't_i_create_an_object_that_replaces_an_old_object_with_the_same_name?">Why can't I create an object that replaces an old object with the same name?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_can_i_make_my_simple_type_run_faster?">How can I make my simple type run faster?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#macros">MACROS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_a_macro?">What is a macro?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_are_macros_good_for?">What are macros good for?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_do_conditional_compilation?">How do I do conditional compilation?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#how_do_i_define_new_type_definition_syntax?">How do I define new type definition syntax?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#are_there_are_restrictions_on_macro_names?">Are there are restrictions on macro names?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#keywords">KEYWORDS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#copyright">COPYRIGHT</a><br>
<h2><a name="description">DESCRIPTION</a></h2>
<p>
<p>

<h2><a name="overview">OVERVIEW</a></h2>
<p>

<h3><a name="what_is_this_document?">What is this document?</a></h3>
<p>

This is an atypical FAQ list, in that few of the questions are
frequently asked.  Rather, these are the questions I think a newcomer
to Snit should be asking.  This file is not a complete reference to
Snit, however; that information is in the <b class='cmd'>snit</b> man page.


<h3><a name="what_is_snit?">What is Snit?</a></h3>
<p>

Snit is a framework for defining abstract data types and megawidgets
in pure Tcl.  The name &quot;Snit&quot; original stood for &quot;Snit's Not Incr Tcl&quot;, 
signifying that Snit takes a different approach to defining objects 
than does Incr Tcl, the best known object framework for Tcl.

<p>

The primary purpose of Snit is to be <em>object glue</em>--to help you
compose diverse objects from diverse sources into types and
megawidgets with clean, convenient interfaces so that you can more
easily build your application.

<p>

Snit isn't about theoretical purity or minimalist design; it's about
being able to do powerful things easily and consistently without
having to think about them--so that you can concentrate on building
your application. 

<p>

Snit isn't about implementing thousands of nearly identical
carefully-specified lightweight thingamajigs--not as individual Snit
objects.  Traditional Tcl methods will be much faster, and not much
more complicated.  But Snit <em>is</em> about implementing a clean interface
to manage a collection of thousands of nearly identical
carefully-specified lightweight thingamajigs (e.g., think of the text
widget and text tags, or the canvas widget and canvas objects).  Snit
lets you hide the details of just how those thingamajigs are
stored--so that you can ignore it, and concentrate on building your
application. 

<p>

Snit isn't a way of life, a silver bullet, or the Fountain of
Youth. It's just a way of managing complexity--and of managing some of
the complexity of managing complexity--so that you can concentrate on
building your application. 

<h3><a name="what_version_of_tcl_does_snit_require?">What version of Tcl does Snit require?</a></h3>
<p>

Snit requires version Tcl 8.4 or later.

<h3><a name="where_can_i_download_snit?">Where can I download Snit?</a></h3>
<p>

Snit is part of Tcllib, the standard Tcl library, so you might already
have it.  It's also available at the Snit Home Page,
<a href="http://www.wjduquette.com/snit">http://www.wjduquette.com/snit</a>.

<h3><a name="what_are_snit's_goals?">What are Snit's goals?</a></h3>
<p>

<p>

<ul>
<li>

A Snit object should should be at least as efficient as a hand-coded Tcl
object (see <a href="http://www.wjduquette.com/tcl/objects.html">http://www.wjduquette.com/tcl/objects.html</a>).

<br><br>
<li>

The fact that Snit was used in an object's implementation should be
transparent (and irrelevant) to clients of that object.

<br><br>
<li>

Snit should be able to encapsulate objects from other sources,
particularly Tk widgets.

<br><br>
<li>

Snit megawidgets should be (to the extent possible) indistinguishable
in interface from Tk widgets.

<br><br>
<li>

Snit should be Tclish--that is, rather than trying to emulate C++,
Smalltalk, or anything else, it should try to emulate Tcl itself.

<br><br>
<li>

It should have a simple, easy-to-use, easy-to-remember syntax.

</ul>

<h3><a name="how_is_snit_different_from_other_oo_frameworks?">How is Snit different from other OO frameworks?</a></h3>
<p>

Snit is unique among Tcl object systems in that
it is based not on inheritance but on delegation.  Object
systems based on inheritance only allow you to inherit from classes
defined using the same system, and that's a shame.  In Tcl, an object
is anything that acts like an object; it shouldn't matter how the
object was implemented.  I designed Snit to help me build applications
out of the materials at hand; thus, Snit is designed to be able to
incorporate and build on any object, whether it's a hand-coded object,
a Tk widget, an Incr Tcl object, a BWidget or almost anything else.

<p>

Note that you can achieve the effect of inheritance using 
<a href="#components">COMPONENTS</a> and <a href="#delegation">DELEGATION</a>--and you can inherit
from anything that looks like a Tcl object.

<h3><a name="what_can_i_do_with_snit?">What can I do with Snit?</a></h3>
<p>

Using Snit, a programmer can:

<ul>
<li>

Create abstract data types and Tk megawidgets.

<br><br>
<li>

Define instance variables, type variables, and Tk-style options.

<br><br>
<li>

Define constructors, destructors, instance methods, type methods, procs.

<br><br>
<li>

Assemble a type out of component types.  Instance methods and options
can be delegated to the component types automatically.

</ul>

<h2><a name="objects">OBJECTS</a></h2>
<p>

<h3><a name="what_is_an_object?">What is an object?</a></h3>
<p>

A full description of object-oriented programming is beyond
the scope of this FAQ, obviously.  In simple terms, an object is an instance of
an abstract data type--a coherent bundle of code and data.
There are many ways to represent objects in Tcl/Tk; the best known
examples are the Tk widgets.

<p>

A Tk widget is an object; it is represented by a Tcl command.  
The object's methods are subcommands of the Tcl command.  The object's
properties are options accessed using the <strong>configure</strong> and 
<strong>cget</strong> methods.  Snit uses the same conventions as Tk widgets do.

<h3><a name="what_is_an_abstract_data_type?">What is an abstract data type?</a></h3>
<p>

In computer science terms, an abstract data type is a complex data
structure along with a set of operations--a stack, a queue, a
binary tree, etc--that is to say, in modern terms, an object.  In systems
that include include some form of inheritance the word <em>class</em> is
usually used instead of <em>abstract data type</em>, but as Snit
doesn't implement inheritance as it's ordinarily understood
the older term seems more appropriate.  Sometimes this is called 
<em>object-based</em> programming as opposed to object-oriented
programming.  Note that you can easily create the effect of 
inheritance using <a href="#components">COMPONENTS</a> and 
<a href="#delegation">DELEGATION</a>.

<p>

In Snit, as in Tk, a <em>type</em> is a command that creates instances
-- objects -- which belong to the type.  Most types define some number
of <em>options</em> which can be set at creation time, and usually can be
changed later.

<p>

Further, an <em>instance</em> is also a Tcl command--a command that
gives access to the operations which are defined for that abstract
data type.  Conventionally, the operations are defined as subcommands
of the instance command.  For example, to insert
text into a Tk text widget, you use the text widget's <strong>insert</strong>
subcommand:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    # Create a text widget and insert some text in it.
    text .mytext -width 80 -height 24
    .mytext insert end &quot;Howdy!&quot;
</pre></td></tr></table></p>
<p>

In this example, <b class='cmd'>text</b> is the <em>type</em> command and
<b class='cmd'>.mytext</b> is the <em>instance</em> command.

<p>

In Snit, object subcommands are generally called 
<a href="#instance_methods">INSTANCE METHODS</a>.

<h3><a name="what_kinds_of_abstract_data_types_does_snit_provide?">What kinds of abstract data types does Snit provide?</a></h3>
<p>

Snit allows you to define three kinds of abstract data types:

<p>

<ul>
<li>

<b class='cmd'>snit::type</b>
<li>

<b class='cmd'>snit::widget</b>
<li>

<b class='cmd'>snit::widgetadaptor</b>
</ul>


<h3><a name="what_is_a_snit::type?">What is a snit::type?</a></h3>
<p>

A <b class='cmd'>snit::type</b> is a non-GUI abstract data type, e.g., a stack or a
queue.  <b class='cmd'>snit::types</b> are defined using the <b class='cmd'>snit::type</b>
command.  For example, if you were designing a kennel management
system for a dog breeder, you'd need a dog type.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    # ...
}
::dog
%
</pre></td></tr></table></p>
<p>

This definition defines a new command (<b class='cmd'>::dog</b>, in this case)
that can be used to define dog objects.

<p>

An instance of a <b class='cmd'>snit::type</b> can have <a href="#instance_methods">INSTANCE METHODS</a>,
<a href="#instance_variables">INSTANCE VARIABLES</a>, <a href="#options">OPTIONS</a>, and <a href="#components">COMPONENTS</a>.
The type itself can have <a href="#type_methods">TYPE METHODS</a>,
<a href="#type_variables">TYPE VARIABLES</a>, <a href="#type_components">TYPE COMPONENTS</a>, and 
<a href="#procs">PROCS</a>. 


<h3><a name="what_is_a_snit::widget?">What is a snit::widget?</a></h3>
<p>

A <b class='cmd'>snit::widget</b> is a Tk megawidget built using Snit; it is very
similar to a <b class='cmd'>snit::type</b>.  See <a href="#widgets">WIDGETS</a>.


<h3><a name="what_is_a_snit::widgetadaptor?">What is a snit::widgetadaptor?</a></h3>
<p>

A <b class='cmd'>snit::widgetadaptor</b> uses Snit to wrap an existing widget type
(e.g., a Tk label), modifying its interface to a lesser or greater
extent.  It is very similar to a <b class='cmd'>snit::widget</b>.
See <a href="#widget_adaptors">WIDGET ADAPTORS</a>.


<h3><a name="how_do_i_create_an_instance_of_a_snit::type?">How do I create an instance of a snit::type?</a></h3>
<p>

You create an instance of a <b class='cmd'>snit::type</b> by passing the new
instance's name to the type's create method.  In the following
example, we create a <b class='cmd'>dog</b> object called <b class='cmd'>spot</b>.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    # ....
}
::dog
% dog create spot
::spot
%
</pre></td></tr></table></p>
<p>

In general, the <strong>create</strong> method name can be omitted so long as
the instance name doesn't conflict with any defined 
<a href="#type_methods">TYPE METHODS</a>. (See <a href="#type_components">TYPE COMPONENTS</a> for the
special case in which this doesn't work.)
So the following example is identical to the
previous example:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    # ....
}
::dog
% dog spot
::spot
%
</pre></td></tr></table></p>
<p>

This document generally uses the shorter form.

<p>

If the <b class='cmd'>dog</b> type defines <a href="#options">OPTIONS</a>, these can usually be 
given defaults at creation time:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    option -breed mongrel
    option -color brown

    method bark {} { return &quot;$self barks.&quot; }
}
::dog
% dog create spot -breed dalmation -color spotted
::spot
% spot cget -breed
dalmation
% spot cget -color
spotted
%
</pre></td></tr></table></p>
<p>

Once created, the instance name now names a new Tcl command that is used
to manipulate the object.  For example, the following code makes the
dog bark:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% spot bark
::spot barks.
%
</pre></td></tr></table></p>
<p>

<h3><a name="how_do_i_refer_to_an_object_indirectly?">How do I refer to an object indirectly?</a></h3>
<p>

Some programmers prefer to save the object name in a variable, and
reference it that way.  For example,

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog { ... }
::dog
% set d [dog spot -breed dalmation -color spotted]
::spot
% $d cget -breed
dalmation
% $d bark
::spot barks.
%
</pre></td></tr></table></p>
<p>

If you prefer this style, you might prefer to have Snit
generate the instance's name automatically.

<h3><a name="how_can_i_generate_the_object_name_automatically?">How can I generate the object name automatically?</a></h3>
<p>

If you'd like Snit to generate an object name for you,
use the <strong>%AUTO%</strong> keyword as the requested name:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog { ... }
::dog
% set d [dog %AUTO%]
::dog2
% $d bark
::dog2 barks.
%
</pre></td></tr></table></p>
<p>

The &quot;%AUTO%&quot; keyword can be embedded in a longer string:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% set d [dog obj_%AUTO%]
::obj_dog4
% $d bark
::obj_dog4 barks.
%
</pre></td></tr></table></p>
<p>


<h3><a name="can_types_be_renamed?">Can types be renamed?</a></h3>
<p>

Tcl's <b class='cmd'>rename</b> command renames other commands.  It's a common
technique in Tcl to modify an existing command by renaming it and
defining a new command with the original name; the new command usually
calls the renamed command.

<p>

<b class='cmd'>snit::type</b> commands, however, should never be renamed; to do so breaks
the connection between the type and its objects.

<h3><a name="can_objects_be_renamed?">Can objects be renamed?</a></h3>
<p>

Tcl's <b class='cmd'>rename</b> command renames other commands.  It's a common
technique in Tcl to modify an existing command by renaming it and
defining a new command with the original name; the new command usually
calls the renamed command.

<p>

All Snit objects (including <em>widgets</em> and <em>widgetadaptors</em>)
can be renamed, though this flexibility has some consequences:

<p>

<ul>
<li>

In an instance method, the implicit argument <strong>self</strong> will always
contain the object's current name, so instance methods can always call
other instance methods using <strong>$self</strong>.

<br><br>
<li>

If the object is renamed, however, then <strong>$self</strong>'s value will change.
Therefore, don't use <strong>$self</strong> for anything that will break if 
<strong>$self</strong> changes. For example, don't pass a callback command to
another object like this:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>.btn configure -command [list $self ButtonPress]
</pre></td></tr></table></p>

You'll get an error if <b class='cmd'>.btn</b> calls your command after your object is
renamed.

<br><br>
<li>

Instead, the your object should define its callback command like this:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>.btn configure -command [mymethod ButtonPress]
</pre></td></tr></table></p>

The <b class='cmd'>mymethod</b> command returns code that will call the desired
method safely; the caller of the callback can add additional
arguments to the end of the command as usual.

<br><br>
<li>

Every object has a private namespace; the name of this namespace is
available in method bodies, etc., as the value of the implicit
argument <strong>selfns</strong>.  This value is constant for the life of the
object.  Use <strong>$selfns</strong> instead of <strong>$self</strong> if you need a 
unique token to identify the object.

<br><br>
<li>

When a <b class='cmd'>snit::widget</b>'s instance command is renamed, its Tk window
name remains the same -- and is still extremely
important. Consequently, the Tk window name is available in 
method bodies as the value of the implicit argument <strong>win</strong>.  
This value is constant for the
life of the object.  When creating child windows, it's best to use
<strong>$win.child</strong> rather than <strong>$self.child</strong> as the name of the
child window.

</ul>

<h3><a name="how_do_i_destroy_a_snit_object?">How do I destroy a Snit object?</a></h3>
<p>

Every instance of a <b class='cmd'>snit::type</b> has a <strong>destroy</strong> method:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog { ... }
::dog
% dog spot
::spot
% spot bark
::spot barks.
% spot destroy
% spot barks
invalid command name &quot;spot&quot;
%
</pre></td></tr></table></p>
<p>

Snit megawidgets (i.e., instances of <b class='cmd'>snit::widget</b> and
<b class='cmd'>snit::widgetadaptor</b>) are destroyed like any other widget: by
using the Tk <b class='cmd'>destroy</b> command on the widget or on one of its
ancestors in the window hierarchy.

<p>

In addition, any Snit object of any type can be destroyed by renaming
it to the empty string using the Tcl <b class='cmd'>rename</b> command.

<p>

Finally, every Snit type has a type method called <strong>destroy</strong>; calling it
destroys the type and all of its instances:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog { ... }
::dog
% dog spot
::spot
% spot bark
::spot barks.
% dog destroy
% spot bark
invalid command name &quot;spot&quot;
% dog fido
invalid command name &quot;dog&quot;
%
</pre></td></tr></table></p>

<h2><a name="instance_methods">INSTANCE METHODS</a></h2>
<p>

<h3><a name="what_is_an_instance_method?">What is an instance method?</a></h3>
<p>

An instance method is a procedure associated with a specific object
and called as a subcommand of the object's command.  It is given free
access to all of the object's type variables, instance variables, and
so forth.

<h3><a name="how_do_i_define_an_instance_method?">How do I define an instance method?</a></h3>
<p>

Instance methods are defined in the type definition using
the <b class='cmd'>method</b> statement.  Consider the following code that might be
used to add dogs to a computer simulation:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    method bark {} {
        return &quot;$self barks.&quot;
    }

    method chase {thing} {
        return &quot;$self chases $thing.&quot;
    }
}
::dog
%
</pre></td></tr></table></p>
<p>

A dog can bark, and it can chase things.

<p>

The <b class='cmd'>method</b> statement looks just like a normal Tcl <b class='cmd'>proc</b>,
except that it appears in a <b class='cmd'>snit::type</b> definition.  Notice that
every instance method gets an implicit argument called <strong>self</strong>;
this argument contains the object's name.  (There's more on
implicit method arguments below.)

<h3><a name="how_does_a_client_call_an_instance_method?">How does a client call an instance method?</a></h3>
<p>

The method name becomes a subcommand of the object.  For example,
let's put a simulated dog through its paces:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% dog spot
::spot
% spot bark
::spot barks.
% spot chase cat
::spot chases cat.
%
</pre></td></tr></table></p>
<p>

<h3><a name="how_does_an_instance_method_call_another_instance_method?">How does an instance method call another instance method?</a></h3>
<p>

If method A needs to call method B on the same object, it does so just
as a client does: it calls method B as a subcommand of the object
itself, using the object name stored in the implicit argument <strong>self</strong>.

<p>

Suppose, for example, that our dogs never chase anything without
barking at them:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    method bark {} {
        return &quot;$self barks.&quot;
    }

    method chase {thing} {
        return &quot;$self chases $thing.  [$self bark]&quot;
    }
}
::dog
% dog spot
::spot
% spot bark
::spot barks.
% spot chase cat
::spot chases cat.  ::spot barks.
%
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_instance_method_names?">Are there any limitations on instance method names?</a></h3>
<p>

Not really, so long as you avoid the standard instance method names:
<strong>configure</strong>, <strong>configurelist</strong>, <strong>cget</strong>,
<strong>destroy</strong>, and <strong>info</strong>.

<h3><a name="how_do_i_make_an_instance_method_private?">How do I make an instance method private?</a></h3>
<p>

It's often useful to define private methods, that is, instance methods
intended to be called only by other methods of the same object.

<p>

Snit doesn't implement any access control on instance methods, so all
methods are <em>de facto</em> public.  Conventionally, though, the
names of public methods begin with a lower-case letter, and the names
of private methods begin with an upper-case letter.

<p>

For example, suppose our simulated dogs only bark in response to other
stimuli; they never bark just for fun.  So the <strong>bark</strong> method
becomes <strong>Bark</strong> to indicate that it is private:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    # Private by convention: begins with uppercase letter.
    method Bark {} {
        return &quot;$self barks.&quot;
    }

    method chase {thing} {
        return &quot;$self chases $thing. [$self Bark]&quot;
    }
}
::dog
% dog fido
::fido
% fido chase cat
::fido chases cat. ::fido barks.
%
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_instance_method_arguments?">Are there any limitations on instance method arguments?</a></h3>
<p>

Method argument lists are defined just like normal Tcl <b class='cmd'>proc</b> argument
lists; in particular, they can include arguments with default values
 and the <strong>args</strong> argument.

<p>

However, every method also has a number of implicit arguments
provided by Snit in addition to those explicitly defined.  The names
of these implicit arguments may not used to name explicit arguments.

<h3><a name="what_implicit_arguments_are_passed_to_each_instance_method?">What implicit arguments are passed to each instance method?</a></h3>
<p>

The arguments implicitly passed to every method are <strong>type</strong>,
<strong>selfns</strong>, <strong>win</strong>, and <strong>self</strong>.

<h3><a name="what_is_$type?">What is $type?</a></h3>
<p>

The implicit argument <strong>type</strong> contains the fully qualified name of
the object's type:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type thing {
    method mytype {} {
        return $type
    }
}
::thing
% thing something
::something
% something mytype
::thing
%
</pre></td></tr></table></p>
<p>

<h3><a name="what_is_$self?">What is $self?</a></h3>
<p>

The implicit argument <strong>self</strong> contains the object's fully
qualified name.

<p>

If the object's command is renamed, then <strong>$self</strong> will change to
match in subsequent calls.  Thus, your code should not assume that
<strong>$self</strong> is constant unless you know for sure that the object
will never be renamed.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type thing {
    method myself {} {
        return $self
    }
}
::thing
% thing mutt
::mutt
% mutt myself
::mutt
% rename mutt jeff
% jeff myself
::jeff
%
</pre></td></tr></table></p>
<p>

<h3><a name="what_is_$selfns?">What is $selfns?</a></h3>
<p>

Each Snit object has a private namespace in which to store its
<a href="#instance_variables">INSTANCE VARIABLES</a> and <a href="#options">OPTIONS</a>.  The implicit argument
<strong>selfns</strong> contains the name of this namespace; its value never changes, and
is constant for the life of the object, even if the object's name
changes:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type thing {
    method myNameSpace {} {
        return $selfns
    }
}
::thing
% thing jeff
::jeff
% jeff myNameSpace
::thing::Snit_inst3
% rename jeff mutt
% mutt myNameSpace
::thing::Snit_inst3
%
</pre></td></tr></table></p>
<p>

The above example reveals how Snit names an instance's private
namespace; however, you should not write code that depends on the
specific naming convention, as it might change in future releases.

<h3><a name="what_is_$win?">What is $win?</a></h3>
<p>

The implicit argument <strong>win</strong> is defined for all Snit methods,
though it really makes sense only for those of 
<a href="#widgets">WIDGETS</a> and <a href="#widget_adaptors">WIDGET ADAPTORS</a>.  <strong>$win</strong> is simply
the original name of the object, whether it's been renamed or not.
For widgets and widgetadaptors, it is also therefore the name of a Tk
window.

<p>

When a <b class='cmd'>snit::widgetadaptor</b> is used to modify the interface of a
widget or megawidget, it must rename the widget's original command and
replace it with its own.

<p>

Thus, using <strong>win</strong> whenever the Tk window name is called for
means that a <b class='cmd'>snit::widget</b> or <b class='cmd'>snit::widgetadaptor</b> can be
adapted by a <b class='cmd'>snit::widgetadaptor</b>.  See <a href="#widgets">WIDGETS</a> for
more information.

<h3><a name="how_do_i_pass_an_instance_method_as_a_callback?">How do I pass an instance method as a callback?</a></h3>
<p>

It depends on the context.

<p>

Suppose in my application I have a <b class='cmd'>dog</b> object named <b class='cmd'>fido</b>,
and I want <b class='cmd'>fido</b> to bark when a Tk button called <b class='cmd'>.bark</b> is 
pressed.  In this case, I create the callback command in the usual
way, using <b class='cmd'>list</b>:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    button .bark -text &quot;Bark!&quot; -command [list fido bark]
</pre></td></tr></table></p>
<p>

In typical Tcl style, we use a callback to hook two independent
components together.  But suppose that the <b class='cmd'>dog</b> object has
a graphical interface and owns the button itself?  In this case,
the <b class='cmd'>dog</b> must pass one of its own instance methods to the
button it owns.  The obvious thing to do is this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::widget dog {
    constructor {args} {
        #...
        button $win.barkbtn -text &quot;Bark!&quot; -command [list $self bark]
        #...
    }
}
::dog
%
</pre></td></tr></table></p>
<p>

(Note that in this example, our <b class='cmd'>dog</b>
becomes a <b class='cmd'>snit::widget</b>, because it has GUI behavior.  See
<a href="#widgets">WIDGETS</a> for more.)  Thus, if we create a <b class='cmd'>dog</b> called
<b class='cmd'>.spot</b>, it will create a Tk button called <b class='cmd'>.spot.barkbtn</b>;
when pressed, the button will call <b class='cmd'>$self bark</b>.

<p>

Now, this will work--provided that <b class='cmd'>.spot</b> is never renamed to
something else.  But surely renaming widgets is
abnormal?  And so it is--unless <b class='cmd'>.spot</b> is the hull component of a
<b class='cmd'>snit::widgetadaptor</b>.  If it is, then it will be renamed, and
<b class='cmd'>.spot</b> will become the name of the <b class='cmd'>snit::widgetadaptor</b>
object.  When the button is pressed, the command <b class='cmd'>$self bark</b>
will be handled by the <b class='cmd'>snit::widgetadaptor</b>, which might or might
not do the right thing.

<p>

There's a safer way to do it, and it looks like this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::widget dog {
    constructor {args} {
        #...
        button $win.barkbtn -text &quot;Bark!&quot; -command [mymethod bark]
        #...
    }
}
::dog
%
</pre></td></tr></table></p>
<p>

The command <b class='cmd'>mymethod</b> takes any number of arguments, and can be
used like <b class='cmd'>list</b> to build up a callback command; the only
difference is that <b class='cmd'>mymethod</b> returns a 
form of the command that won't change even if the instance's name changes.

<h3><a name="how_do_i_delegate_instance_methods_to_a_component?">How do I delegate instance methods to a component?</a></h3>
<p>

See <a href="#delegation">DELEGATION</a>.

<h2><a name="instance_variables">INSTANCE VARIABLES</a></h2>
<p>


<h3><a name="what_is_an_instance_variable?">What is an instance variable?</a></h3>
<p>

An instance variable is a private variable associated with some
particular Snit object.  Instance variables can be scalars or arrays.


<h3><a name="how_is_a_scalar_instance_variable_defined?">How is a scalar instance variable defined?</a></h3>
<p>

Scalar instance variables are defined in the type definition using the
<b class='cmd'>variable</b> statement.  You can simply name it, or you can
initialize it with a value:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    # Define variable &quot;greeting&quot; and initialize it with &quot;Howdy!&quot;
    variable greeting &quot;Howdy!&quot;
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_is_an_array_instance_variable_defined?">How is an array instance variable defined?</a></h3>
<p>

Array instance variables are also defined in the type definition
using the <b class='cmd'>variable</b> command.  You can initialize them at the same
time by specifying the <strong>-array</strong> option:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    # Define array variable &quot;greetings&quot;
    variable greetings -array {
        formal &quot;Good Evening&quot;
        casual &quot;Howdy!&quot;
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_instance_variable_names?">Are there any limitations on instance variable names?</a></h3>
<p>

Just a few.

<p>

First, every Snit object has a built-in instance variable called
<strong>options</strong>, which should never be redefined.

<p>

Second, all names beginning with &quot;Snit_&quot; are reserved for
use by Snit internal code.

<p>

Third, instance variable names containing the namespace delimiter
(<strong>::</strong>) are likely to cause great confusion.


<h3><a name="do_i_need_to_declare_my_instance_variables_in_my_methods?">Do I need to declare my instance variables in my methods?</a></h3>
<p>

No. Once you've defined an instance variable in the type definition,
it can be used in any instance code (instance methods, the
constructor, and the destructor) without declaration.  This differs
from normal Tcl practice, in which all non-local variables in a proc
need to be declared.

<h3><a name="how_do_i_pass_an_instance_variable's_name_to_another_object?">How do I pass an instance variable's name to another object?</a></h3>
<p>

In Tk, it's common to pass a widget a variable name; for example, Tk
label widgets have a <strong>-textvariable</strong> option which names the
variable which will contain the widget's text.  This allows the
program to update the label's value just by assigning a new value to
the variable.

<p>

If you naively pass the instance variable name to the label widget,
you'll be confused by the result; Tk will assume that the name names a
global variable.  Instead, you need to provide a fully-qualified
variable name.  From within an instance method or a constructor, you
can fully qualify the variable's name using the <b class='cmd'>myvar</b> command:

<p> 
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    variable labeltext &quot;&quot;

    constructor {args} {
        # ...

        label $win.label -textvariable [myvar labeltext]

        # ...
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_do_i_make_an_instance_variable_public?">How do I make an instance variable public?</a></h3>
<p>

Practically speaking, you don't.  Instead, you'll implement public
variables as <a href="#options">OPTIONS</a>.

Alternatively, you can write <a href="#instance_methods">INSTANCE METHODS</a> to set and get
the variable's value.

<h2><a name="options">OPTIONS</a></h2>
<p>

<h3><a name="what_is_an_option?">What is an option?</a></h3>
<p>

A type's options are the equivalent of what other object-oriented
languages would call public member variables or properties: they are
data values which can be retrieved and (usually) set by the clients of
an object.

<p>

Snit's implementation of options follows the Tk model fairly exactly,
except that <b class='cmd'>snit::type</b> objects usually don't interact with 
<a href="#the_tk_option_database">THE TK OPTION DATABASE</a>; <b class='cmd'>snit::widget</b> and 
<b class='cmd'>snit::widgetadaptor</b> objects, on the other hand, always do.

<h3><a name="how_do_i_define_an_option?">How do I define an option?</a></h3>
<p>

Options are defined in the type definition using the <b class='cmd'>option</b>
statement.  Consider the following type, to be used in an application
that manages a list of dogs for a pet store:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -breed -default mongrel
    option -color -default brown
    option -akc   -default 0
    option -shots -default 0
}
</pre></td></tr></table></p>
<p>


According to this, a dog has four notable properties: a
breed, a color, a flag that says whether it's pedigreed with the
American Kennel Club, and another flag that says whether it has had
its shots.  The default dog, evidently, is a brown mutt.

<p>

There are a number of options you can specify when defining an option;
if <strong>-default</strong> is the only one, you can omit the word 
<strong>-default</strong> as follows:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -breed mongrel
    option -color brown
    option -akc   0
    option -shots 0
}
</pre></td></tr></table></p>

<p>

If no <strong>-default</strong> value is specified, the option's default value
will be the empty string (but see <a href="#the_tk_option_database">THE TK OPTION DATABASE</a>).

<p>

The Snit man page refers to options like these as &quot;locally defined&quot; options.

<h3><a name="how_can_a_client_set_options_at_object_creation?">How can a client set options at object creation?</a></h3>
<p>

The normal convention is that the client may pass any number of
options and their values after the object's name at object creation.
For example, the <b class='cmd'>::dog</b> command defined in the previous answer can now
be used to create individual dogs.  Any or all of the options may be
set at creation time.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% dog spot -breed beagle -color &quot;mottled&quot; -akc 1 -shots 1
::spot
% dog fido -shots 1
::fido
%
</pre></td></tr></table></p>
<p>

So ::spot is a pedigreed beagle; ::fido is a typical mutt, but his
owners evidently take care of him, because he's had his shots.

<p>

<em>Note:</em> If the type defines a constructor, it can specify a
different object-creation syntax.  See <a href="#constructors">CONSTRUCTORS</a> for more
information.

<h3><a name="how_can_a_client_retrieve_an_option's_value?">How can a client retrieve an option's value?</a></h3>
<p>

Retrieve option values using the <strong>cget</strong> method:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% spot cget -color
mottled
% fido cget -breed
mongrel
%
</pre></td></tr></table></p>
<p>

<h3><a name="how_can_a_client_set_options_after_object_creation?">How can a client set options after object creation?</a></h3>
<p>

Any number of options may be set at one time using the
<strong>configure</strong> instance method.  Suppose that closer inspection
shows that ::fido is not a brown mongrel, but rather a rare Arctic Boar 
Hound of a lovely dun color:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% fido configure -color dun -breed &quot;Arctic Boar Hound&quot;
% fido cget -color
dun
% fido cget -breed
Arctic Boar Hound
</pre></td></tr></table></p>
<p>

Alternatively, the <strong>configurelist</strong> method takes a list of
options and values; occasionally this is more convenient:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% set features [list -color dun -breed &quot;Arctic Boar Hound&quot;]
-color dun -breed {Arctic Boar Hound}
% fido configurelist $features
% fido cget -color
dun
% fido cget -breed
Arctic Boar Hound
%
</pre></td></tr></table></p>
<p>

<h3><a name="how_should_an_instance_method_access_an_option_value?">How should an instance method access an option value?</a></h3>
<p>

There are two ways an instance method can set and retrieve an option's
value.  One is to use the <strong>configure</strong> and <strong>cget</strong>
methods, as shown below.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    option -weight 10

    method gainWeight {} {
        set wt [$self cget -weight]
        incr wt
        $self configure -weight $wt
    }
}
::dog
% dog fido
::fido
% fido cget -weight
10
% fido gainWeight
% fido cget -weight
11
%
</pre></td></tr></table></p>
<p>

Alternatively, Snit provides a built-in array instance variable called
<strong>options</strong>.  The indices are the option names; the values are the
option values.  The method <strong>gainWeight</strong> can thus be rewritten as
follows:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
    method gainWeight {
        incr options(-weight)
    }
</pre></td></tr></table></p>
<p>

As you can see, using the <strong>options</strong> variable involves considerably
less typing and is the usual way to do it.  But if you use 
<strong>-configuremethod</strong> or <strong>-cgetmethod</strong> (described in the following
answers), you might wish to use the <strong>configure</strong> and 
<strong>cget</strong> methods anyway, just so that any special processing you've
implemented is sure to get done.

<h3><a name="how_can_i_make_an_option_read-only?">How can I make an option read-only?</a></h3>
<p>

Define the option with <strong>-readonly yes</strong>.

<p>

Suppose you've got an option that determines how
instances of your type are constructed; it must be set at creation
time, after which it's constant.  For example, a dog never changes its
breed; it might or might not have had its shots.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    option -breed -default mongrel -readonly yes
    option -shots -default no
}
::dog
% dog fido -breed retriever
::fido
% fido configure -shots yes
% fido configure -breed terrier
option -breed can only be set at instance creation
%
</pre></td></tr></table></p>
<p>

<h3><a name="how_can_i_catch_accesses_to_an_option's_value?">How can I catch accesses to an option's value?</a></h3>
<p>

Define a <strong>-cgetmethod</strong> for the option.

<h3><a name="what_is_a_-cgetmethod?">What is a -cgetmethod?</a></h3>
<p>

A <strong>-cgetmethod</strong> is a method that's called whenever the related
option's value is queried via the 
<strong>cget</strong> instance method.  The handler can compute the option's
value, retrieve it from a database, or do anything else you'd like it to do.

<p>

Here's what the default behavior would look like if
written using a <strong>-cgetmethod</strong>:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -color -default brown -cgetmethod GetOption

    method GetOption {option} {
        return $options($option)
    }
}
</pre></td></tr></table></p>
<p>

Any instance method can be used, provided that it takes one argument,
the name of the option whose value is to be retrieved.

<h3><a name="how_can_i_catch_changes_to_an_option's_value?">How can I catch changes to an option's value?</a></h3>
<p>

Define a <strong>-configuremethod</strong> for the option.

<h3><a name="what_is_a_-configuremethod?">What is a -configuremethod?</a></h3>
<p>

A <strong>-configuremethod</strong> is a method that's called whenever the
related option is given a new value via the <strong>configure</strong> or
<strong>configurelist</strong> instance methods. The method can 
pass the value on to some other object, store it in a database, or do
anything else you'd like it to do.

<p>

Here's what the default configuration behavior would look like if
written using a <strong>-configuremethod</strong>:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -color -default brown -configuremethod SetOption

    method SetOption {option value} {
        set options($option) $value
    }
}
</pre></td></tr></table></p>
<p>

Any instance method can be used, provided that it takes two arguments,
the name of the option and the new value.

<p>

Note that if your method doesn't store the value in the <strong>options</strong>
array, the <strong>options</strong> array won't get updated.

<h3><a name="how_can_i_validate_an_option's_value?">How can I validate an option's value?</a></h3>
<p>

Define a <strong>-validatemethod</strong>.

<h3><a name="what_is_a_-validatemethod?">What is a -validatemethod?</a></h3>
<p>

A <strong>-validatemethod</strong> is a method that's called whenever the
related option is given a new value via the <strong>configure</strong> or
<strong>configurelist</strong> instance methods.  It's the method's
responsibility to determine whether the new value is valid, and throw
an error if it isn't.  The <strong>-validatemethod</strong>, if any, is called
before the value is stored in the <strong>options</strong> array; in particular,
it's called before the <strong>-configuremethod</strong>, if any.

<p>

For example, suppose an option always takes a Boolean value.  You can
ensure that the value is in fact a valid Boolean like this:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    option -shots -default no -validatemethod BooleanOption

    method BooleanOption {option value} {
        if {![string is boolean -strict $value]} {
            error &quot;expected a boolean value, got \&quot;$value\&quot;&quot;
        }
    }
}
::dog
% dog fido
% fido configure -shots yes
% fido configure -shots NotABooleanValue
expected a boolean value, got &quot;NotABooleanValue&quot;
%
</pre></td></tr></table></p>

Note that the same <strong>-validatemethod</strong> can be used to validate any number
of boolean options.

<p>

Any method can be a <strong>-validatemethod</strong> provided that it takes
two arguments, the option name and the new option value.


<h2><a name="type_variables">TYPE VARIABLES</a></h2>
<p>

<h3><a name="what_is_a_type_variable?">What is a type variable?</a></h3>
<p>

A type variable is a private variable associated with a Snit type
rather than with a particular instance of the type.  In C++ and Java,
the term <em>static member variable</em> is used for the same notion.
Type variables can be scalars or arrays.


<h3><a name="how_is_a_scalar_type_variable_defined?">How is a scalar type variable defined?</a></h3>
<p>

Scalar type variables are defined in the type definition using the
<b class='cmd'>typevariable</b> statement.  You can simply name it, or you can
initialize it with a value:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
snit::type mytype {
    # Define variable &quot;greeting&quot; and initialize it with &quot;Howdy!&quot;
    typevariable greeting &quot;Howdy!&quot;
}
</pre></td></tr></table></p>
<p>

Every object of type <b class='cmd'>mytype</b> now has access to a single variable
called <strong>greeting</strong>.

<h3><a name="how_is_an_array-valued_type_variable_defined?">How is an array-valued type variable defined?</a></h3>
<p>

Array-valued type variables are also defined using the
<b class='cmd'>typevariable</b> command; to initialize them, include the 
<strong>-array</strong> option:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    # Define typearray variable &quot;greetings&quot;
    typevariable greetings -array {
        formal &quot;Good Evening&quot;
        casual &quot;Howdy!&quot;
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_type_variable_names?">Are there any limitations on type variable names?</a></h3>
<p>

Type variable names have the same restrictions as 
the names of <a href="#instance_variables">INSTANCE VARIABLES</a> do.

<h3><a name="do_i_need_to_declare_my_type_variables_in_my_methods?">Do I need to declare my type variables in my methods?</a></h3>
<p>

No. Once you've defined a type variable in the type definition, it can
be used in <a href="#instance_methods">INSTANCE METHODS</a> or <a href="#type_methods">TYPE METHODS</a> without
declaration.  This differs from normal Tcl practice, in which all
non-local variables in a proc need to be declared.

<h3><a name="how_do_i_pass_a_type_variable's_name_to_another_object?">How do I pass a type variable's name to another object?</a></h3>
<p>

In Tk, it's common to pass a widget a variable name; for example, Tk
label widgets have a <strong>-textvariable</strong> option which names the
variable which will contain the widget's text.  This allows the
program to update the label's value just by assigning a new value to
the variable.

<p>

If you naively pass a type variable name to the label widget, you'll
be confused by the result; Tk will assume that the name names a global
variable.  Instead, you need to provide a fully-qualified variable
name.  From within an instance method or a constructor, you can fully
qualify the type variable's name using the <b class='cmd'>mytypevar</b> command:

<p> 
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    typevariable labeltext &quot;&quot;

    constructor {args} {
        # ...

        label $win.label -textvariable [mytypevar labeltext]

        # ...
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_do_i_make_a_type_variable_public?">How do I make a type variable public?</a></h3>
<p>

There are two ways to do this.  The preferred way is to write a pair
of <a href="#type_methods">TYPE METHODS</a> to set and query the type variable's value.

<p>

Type variables are stored in the type's namespace, which has
the same name as the type itself.  Thus, you can also
publicize the type variable's name in your
documentation so that clients can access it directly.  For example,

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    typevariable myvariable
}

set ::mytype::myvariable &quot;New Value&quot;
</pre></td></tr></table></p>
<p>

<h2><a name="type_methods">TYPE METHODS</a></h2>
<p>

<h3><a name="what_is_a_type_method?">What is a type method?</a></h3>
<p>

A type method is a procedure associated with the type itself rather
than with any specific instance of the type, and called as a
subcommand of the type command.

<h3><a name="how_do_i_define_a_type_method?">How do I define a type method?</a></h3>
<p>

Type methods are defined in the type definition using the

<b class='cmd'>typemethod</b> statement:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    # List of pedigreed dogs
    typevariable pedigreed

    typemethod pedigreedDogs {} {
        return $pedigreed
    }
}
</pre></td></tr></table></p>
<p>

Suppose the <b class='cmd'>dog</b> type maintains a list of the names of the dogs
that have pedigrees.  The <b class='cmd'>pedigreedDogs</b> type method returns this
list.

<p>

The <b class='cmd'>typemethod</b> statement looks just like a normal Tcl
<b class='cmd'>proc</b>, except that it appears in a <b class='cmd'>snit::type</b> definition.
Notice that every type method gets an implicit argument called
<strong>type</strong>, which contains the fully-qualified type name.

<h3><a name="how_does_a_client_call_a_type_method?">How does a client call a type method?</a></h3>
<p>

The type method name becomes a subcommand of the type's command.  For
example, assuming that the constructor adds each pedigreed dog to the
list of <strong>pedigreedDogs</strong>,

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -pedigreed 0

    # List of pedigreed dogs
    typevariable pedigreed

    typemethod pedigreedDogs {} {
        return $pedigreed
    }

    # ...
}

dog spot -pedigreed 1
dog fido

foreach dog [dog pedigreedDogs] { ... }
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_type_method_names?">Are there any limitations on type method names?</a></h3>
<p>

Not really, so long as you avoid the standard type method names:

<p>
<strong>create</strong>, <strong>destroy</strong>, and <strong>info</strong>.


<h3><a name="how_do_i_make_a_type_method_private?">How do I make a type method private?</a></h3>
<p>

It's sometimes useful to define private type methods, that is, type
methods intended to be called only by other type or instance methods
of the same object.

<p>

Snit doesn't implement any access control on type methods; by
convention, the names of public methods begin with a lower-case
letter, and the names of private methods begin with an upper-case
letter.

<p>

Alternatively, a Snit <b class='cmd'>proc</b> can be used as a private type method; see
<a href="#procs">PROCS</a>.


<h3><a name="are_there_any_limitations_on_type_method_arguments?">Are there any limitations on type method arguments?</a></h3>
<p>

Method argument lists are defined just like normal Tcl proc argument
lists; in particular, they can include arguments with default values
and the <strong>args</strong> argument.

<p>

However, every type method is called with an implicit argument called
<strong>type</strong> that contains the name of the type command.  In addition,
type methods should by convention avoid using the names of the
arguments implicitly defined for <a href="#instance_methods">INSTANCE METHODS</a>.

<h3><a name="how_does_an_instance_or_type_method_call_a_type_method?">How does an instance or type method call a type method?</a></h3>
<p>

If an instance or type method needs to call a type method, it should
use <strong>$type</strong> to do so:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {

    typemethod pedigreedDogs {} { ... }

    typemethod printPedigrees {} {
        foreach obj [$type pedigreedDogs] { ... }
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_do_i_pass_a_type_method_as_a_callback?">How do I pass a type method as a callback?</a></h3>
<p>

It's common in Tcl to pass a snippet of code to another object, for it
to call later.  Because types cannot be renamed, you can just
use the type name, or, if the callback is registered from within
a type method, <strong>type</strong>.  For example, suppose we want to print a
list of pedigreed dogs when a Tk button is pushed:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
button .btn -text &quot;Pedigrees&quot; -command [list dog printPedigrees]
pack .btn
</pre></td></tr></table></p>

Alternatively, from a method or type method you can use the 
<b class='cmd'>mytypemethod</b> command, just as you would use <b class='cmd'>mymethod</b>
to define a callback command for an <b>INSTANCE METHOD</b>.

<h2><a name="procs">PROCS</a></h2>
<p>

<h3><a name="what_is_a_proc?">What is a proc?</a></h3>
<p>

A Snit <b class='cmd'>proc</b> is really just a Tcl proc defined within the type's
namespace.  You can use procs for private code that isn't related to
any particular instance. 

<h3><a name="how_do_i_define_a_proc?">How do I define a proc?</a></h3>
<p>

Procs are defined by including a <b class='cmd'>proc</b> statement in the type
definition:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    # Pops and returns the first item from the list stored in the
    # listvar, updating the listvar
   proc pop {listvar} { ... }

   # ...
}
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_proc_names?">Are there any limitations on proc names?</a></h3>
<p>

Any name can be used, so long as it does not begin with <strong>Snit_</strong>;
names beginning with <strong>Snit_</strong> are reserved for Snit's own use.
However, the wise programmer will avoid <b class='cmd'>proc</b> names (<b class='cmd'>set</b>,
<b class='cmd'>list</b>, <b class='cmd'>if</b>, etc.) that would shadow standard Tcl
command names.

<p>

<b class='cmd'>proc</b> names, being private, should begin with a capital letter according
to convention; however, as there are typically no public <b class='cmd'>proc</b>s
in the type's namespace it doesn't matter much either way.

<h3><a name="how_does_a_method_call_a_proc?">How does a method call a proc?</a></h3>
<p>

Just like it calls any Tcl command.  For example,

<p>

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mytype {
    # Pops and returns the first item from the list stored in the
    # listvar, updating the listvar
    proc pop {listvar} { ... }

    variable requestQueue {}

    # Get one request from the queue and process it.
    method processRequest {} {
        set req [pop requestQueue]
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_can_i_pass_a_proc_to_another_object_as_a_callback?">How can I pass a proc to another object as a callback?</a></h3>
<p>

The <b class='cmd'>myproc</b> command returns a callback command for the 
<b class='cmd'>proc</b>, just as <b class='cmd'>mymethod</b> does for a method.

<h2><a name="type_constructors">TYPE CONSTRUCTORS</a></h2>
<p>

<h3><a name="what_is_a_type_constructor?">What is a type constructor?</a></h3>
<p>

A type constructor is a body of code that initializes the type as a
whole, rather like a C++ static initializer.  The body of a type
constructor is executed once when the type is defined, and never
again.

<p>

A type can have at most one type constructor.


<h3><a name="how_do_i_define_a_type_constructor?">How do I define a type constructor?</a></h3>
<p>

A type constructor is defined by using the <b class='cmd'>typeconstructor</b>
statement in the type definition.  For example, suppose the type uses
an array-valued type variable as a look-up table, and the values in
the array have to be computed at start-up.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type mytype {
    typevariable lookupTable

    typeconstructor {
        array set lookupTable {key value...}
    }
}
</pre></td></tr></table></p>
<p>



<h2><a name="constructors">CONSTRUCTORS</a></h2>
<p>

<h3><a name="what_is_a_constructor?">What is a constructor?</a></h3>
<p>

In object-oriented programming, an object's constructor is responsible
for initializing the object completely at creation time. The constructor
receives the list of options passed to the <b class='cmd'>snit::type</b> command's
<strong>create</strong> method and can then do whatever it likes.  That might include
computing instance variable values, reading data from files, creating
other objects, updating type and instance variables, and so forth.

<p>

The constructor's return value is ignored (unless it's an
error, of course).


<h3><a name="how_do_i_define_a_constructor?">How do I define a constructor?</a></h3>
<p>

A constructor is defined by using the <b class='cmd'>constructor</b> statement in
the type definition.  Suppose that it's desired to keep a list of all
pedigreed dogs.  The list can be maintained in a 
type variable and retrieved by a type method.  Whenever a dog is
created, it can add itself to the list--provided that it's registered
with the American Kennel Club.

<p> 
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    option -akc 0

    typevariable akcList {}

    constructor {args} {
        $self configurelist $args

        if {$options(-akc)} {
            lappend akcList $self
        }
    }

    typemethod akclist {} {
        return $akcList
    }
}
::dog
% dog spot -akc 1
::spot
% dog fido
::fido
% dog akclist
::spot
%
</pre></td></tr></table></p>
<p>

<h3><a name="what_does_the_default_constructor_do?">What does the default constructor do?</a></h3>
<p>

If you don't provide a constructor explicitly, you get the default
constructor, which is identical to the explicitly-defined
constructor shown here:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    constructor {args} {
        $self configurelist $args
    }
}
</pre></td></tr></table></p>
<p>

When the constructor is called, <strong>args</strong> will be set to the list of
arguments that follow the object's name.  The constructor is allowed
to interpret this list any way it chooses; the normal convention is
to assume that it's a list of option names and values, as shown in the
example above.  If you simply want to save the option values, you
should use the <strong>configurelist</strong> method, as shown.

<h3><a name="can_i_choose_a_different_set_of_arguments_for_the_constructor?">Can I choose a different set of arguments for the constructor?</a></h3>
<p>

Yes, you can.  For example, suppose we wanted to be sure that the
breed was explicitly stated for every dog at creation time, and
couldn't be changed thereafter.  One way to do that is as follows:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    variable breed

    option -color brown
    option -akc 0

    constructor {theBreed args} {
        set breed $theBreed
        $self configurelist $args
    }

    method breed {} { return $breed }
}
::dog
% dog spot dalmatian -color spotted -akc 1
::spot
% spot breed
dalmatian
</pre></td></tr></table></p>
<p>

The drawback is that this syntax is non-standard, and may
limit the compatibility of your new type with other people's code.
For example, Snit assumes that it can create 
<a href="#components">COMPONENTS</a> using the standard creation syntax.

<h3><a name="are_there_any_limitations_on_constructor_arguments?">Are there any limitations on constructor arguments?</a></h3>
<p>

Constructor argument lists are subject to the same limitations
as those on instance method argument lists.  It has the
same implicit arguments, and can contain default values and the 
<strong>args</strong> argument.

<h3><a name="is_there_anything_special_about_writing_the_constructor?">Is there anything special about writing the constructor?</a></h3>
<p>

Yes.  Writing the constructor can be tricky if you're delegating
options to components, and there are specific issues relating to
<b class='cmd'>snit::widget</b>s and <b class='cmd'>snit::widgetadaptor</b>s.  See 
<a href="#delegation">DELEGATION</a>, <a href="#widgets">WIDGETS</a>, 
<a href="#widget_adaptors">WIDGET ADAPTORS</a>, and <a href="#the_tk_option_database">THE TK OPTION DATABASE</a>.

<h2><a name="destructors">DESTRUCTORS</a></h2>
<p>

<h3><a name="what_is_a_destructor?">What is a destructor?</a></h3>
<p>

A destructor is a special kind of method that's called when an object
is destroyed.  It's responsible for doing any necessary clean-up when
the object goes away: destroying <a href="#components">COMPONENTS</a>, closing files, and so
forth.

<h3><a name="how_do_i_define_a_destructor?">How do I define a destructor?</a></h3>
<p>

Destructors are defined by using the <b class='cmd'>destructor</b> statement in the
type definition.

<p>
Suppose we're maintaining a list of pedigreed dogs;
then we'll want to remove dogs from it when they are destroyed.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    option -akc 0

    typevariable akcList {}

    constructor {args} {
        $self configurelist $args

        if {$options(-akc)} {
            lappend akcList $self
        }
    }

    destructor {
        set ndx [lsearch $akcList $self]

        if {$ndx != -1} {
            set akcList [lreplace $akcList $ndx $ndx]
        }
    }

    typemethod akclist {} {
        return $akcList
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="are_there_any_limitations_on_destructor_arguments?">Are there any limitations on destructor arguments?</a></h3>
<p>

Yes; a destructor has no explicit arguments.

<h3><a name="what_implicit_arguments_are_passed_to_the_destructor?">What implicit arguments are passed to the destructor?</a></h3>
<p>

The destructor gets the same implicit arguments that are passed to
<a href="#instance_methods">INSTANCE METHODS</a>: <strong>type</strong>, <strong>selfns</strong>, <strong>win</strong>, and
<strong>self</strong>.

<h3><a name="must_components_be_destroyed_explicitly?">Must components be destroyed explicitly?</a></h3>
<p>

Yes and no.

<p>

Any Tk widgets created by a <b class='cmd'>snit::widget</b> or
<b class='cmd'>snit::widgetadaptor</b> will be destroyed automatically by Tk
when the megawidget is destroyed, in keeping with normal Tk behavior 
(destroying a parent widget destroys the whole tree).

<p>

Components of normal <b class='cmd'>snit::types</b>, on the other hand, 
are never destroyed automatically, nor are non-widget components 
of Snit megawidgets.  If your object creates them in its 
constructor, then it should generally destroy them in its destructor.

<h3><a name="is_there_any_special_about_writing_a_destructor?">Is there any special about writing a destructor?</a></h3>
<p>

Yes.  If an object's constructor throws an error, the object's
destructor will be called to clean up; this means that the object
might not be completely constructed when the destructor is called.
This can cause the destructor to throw its own error; the result
is usually misleading, confusing, and unhelpful.  Consequently, it's
important to write your destructor so that it's fail-safe.  

<p>

For example, a <b class='cmd'>dog</b> might create a <b class='cmd'>tail</b> component; the
component will need to be destroyed.  But suppose there's an error
while processing the creation options--the destructor will be called,
and there will be no <b class='cmd'>tail</b> to destroy.  The simplest solution is
generally to catch and ignore any errors while destroying components.

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    component tail

    constructor {args} {
        $self configurelist $args

        set tail [tail %AUTO%]
    }

    destructor {
        catch {$tail destroy}
    }
}
</pre></td></tr></table></p>


<h2><a name="components">COMPONENTS</a></h2>
<p>

<h3><a name="what_is_a_component?">What is a component?</a></h3>
<p>

Often an object will create and manage a number of other objects.  A
Snit megawidget, for example, will often create a number of Tk
widgets.  These objects are part of the main object; it is composed
of them, so they are called components of the object.

<p>

But Snit also has a more precise meaning for 
<b>COMPONENT</b>.  The components of a Snit object are those
objects to which methods or options can be delegated.
(See <a href="#delegation">DELEGATION</a> for more information about delegation.)

<h3><a name="how_do_i_declare_a_component?">How do I declare a component?</a></h3>
<p>

First, you must decide what role a component plays within your object,
and give the role a name.  Then, you declare the component using its
role name and the <b class='cmd'>component</b> statement.  The <b class='cmd'>component</b>
statement declares an <em>instance variable</em> which is used to
store the component's command name when the component is created.

<p>

For example, suppose your <b class='cmd'>dog</b> object
creates a <b class='cmd'>tail</b> object (the better to wag with, no doubt):

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    component mytail

    constructor {args} {
        # Create and save the component's command
        set mytail [tail %AUTO% -partof $self]
        $self configurelist $args
    }

    method wag {} {
        $mytail wag
    }
}
</pre></td></tr></table></p>
<p>

As shown here, it doesn't matter what the <b class='cmd'>tail</b> object's real
name is; the <b class='cmd'>dog</b> object refers to it by its component name.

<p>

The above example shows one way to delegate the <strong>wag</strong> method to
the <strong>mytail</strong> component; see <a href="#delegation">DELEGATION</a> for an easier way.

<h3><a name="how_is_a_component_named?">How is a component named?</a></h3>
<p>

A component has two names.  The first name is that of the component
variable; this represents the role the component object plays within
the Snit object.  This is the component name proper, and is the name
used to refer to the component within Snit code.  The second name is
the name of the actual component object created by the Snit object's
constructor.  This second name is always a Tcl command name, and is
referred to as the component's object name.

<p>

In the example in the previous question, the component name is 
<strong>mytail</strong>; the <strong>mytail</strong> component's object name is chosen
automatically by Snit since <strong>%AUTO%</strong> was used when the component
object was created.

<h3><a name="are_there_any_limitations_on_component_names?">Are there any limitations on component names?</a></h3>
<p>

Yes.  <b class='cmd'>snit::widget</b> and <b class='cmd'>snit::widgetadaptor</b> have a special
component called the <strong>hull</strong> component; thus, the name <strong>hull</strong>
should be used for no other purpose.

<p>

Otherwise, since component names are in fact instance variable names
they must follow the rules for <a href="#instance_variables">INSTANCE VARIABLES</a>.

<h3><a name="what_is_an_owned_component?">What is an owned component?</a></h3>
<p>

An <em>owned</em> component is a component whose object command's
lifetime is controlled by the <b class='cmd'>snit::type</b> or <b class='cmd'>snit::widget</b>.

<p>

As stated above, a component is an object to
which our object can delegate methods or options.  Under this
definition, our object will usually create its component objects,
but not necessarily.  Consider the following: a dog object has a tail
component; but tail knows that it's part of the dog:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    component mytail

    constructor {args} {
        set mytail [tail %AUTO% -partof $self]
        $self configurelist $args
    }

    destructor {
        catch {$mytail destroy}
    }

    delegate method wagtail to mytail as wag

    method bark {} {
        return &quot;$self barked.&quot;
    }
}

 snit::type tail {
     component mydog
     option -partof -readonly yes

     constructor {args} {
         $self configurelist $args
         set mydog $options(-partof)
     }

     method wag {} {
         return &quot;Wag, wag.&quot;
     }

     method pull {} {
         $mydog bark
     }
 }
</pre></td></tr></table></p>

Thus, if you ask a dog to wag its tail, it tells its tail to wag;
and if you pull the dog's tail, the tail tells the dog to bark.  In
this scenario, the tail is a component of the dog, and the dog is a
component of the tail, but the dog owns the tail and not the other way
around.

<h3><a name="what_does_the_install_command_do?">What does the install command do?</a></h3>
<p>

The <b class='cmd'>install</b> command creates an owned component using a specified
command, and assigns the result to the component's instance variable.
For example:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    component mytail

    constructor {args} {
        # set mytail [tail %AUTO% -partof $self]
        install mytail using tail %AUTO% -partof $self]
        $self configurelist $args
    }
}
</pre></td></tr></table></p>

In a <b class='cmd'>snit::type</b>'s code, the <b class='cmd'>install</b>
command shown above is equivalent to the <strong>set mytail</strong> command
that's commented out.  In a <b class='cmd'>snit::widget</b>'s or 
<b class='cmd'>snit::widgetadaptor</b>'s, code, however, the
<b class='cmd'>install</b> command also queries <a href="#the_tk_option_database">THE TK OPTION DATABASE</a>
and initializes the new component's options accordingly.  For consistency,
it's a good idea to get in the habit of using <b class='cmd'>install</b> for all
owned components.

<h3><a name="must_owned_components_be_created_in_the_constructor?">Must owned components be created in the constructor?</a></h3>
<p>

No, not necessarily.  In fact, there's no reason why an
object can't destroy and recreate a component multiple times over
its own lifetime.

<h3><a name="are_there_any_limitations_on_component_object_names?">Are there any limitations on component object names?</a></h3>
<p>

Yes.

<p>

Component objects which are Tk widgets or megawidgets must have valid
Tk window names.

<p>

Component objects which are not widgets or megawidgets must have
fully-qualified command names, i.e., names which include the full
namespace of the command.  Note that Snit always creates objects with
fully qualified names.

<p>

Next, the object names of components and owned by your object 
must be unique.  This is no problem for widget components, since
widget names are always unique; but consider the following code:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type tail { ... }

snit::type dog {
    delegate method wag to mytail

    constructor {} {
        install mytail using tail mytail
    }
}
</pre></td></tr></table></p>
<p>

This code uses the component name, <strong>mytail</strong>, as the component object
name.  This is not good, and here's why: Snit instance code executes
in the Snit type's namespace.  In this case, the <strong>mytail</strong> component is
created in the <strong>::dog::</strong> namespace, and will thus have the name
<b class='cmd'>::dog::mytail</b>.

<p>

Now, suppose you create two dogs.  Both dogs will attempt to
create a tail called <b class='cmd'>::dog::mytail</b>.  The first will succeed,
and the second will fail, since Snit won't let you create an object if
its name is already a command.  Here are two ways to avoid this situation:

<p>

First, if the component type is a <b class='cmd'>snit::type</b> you can
specify <strong>%AUTO%</strong> as its name, and be guaranteed to get a unique name.
This is the safest thing to do:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
    install mytail using tail %AUTO%
</pre></td></tr></table></p>
<p>

If the component type isn't a <b class='cmd'>snit::type</b> you can base the
component's object name on the type's name in some way:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
     install mytail using tail $self.mytail
</pre></td></tr></table></p>
<p>

This isn't as safe, but should usually work out okay.

<h3><a name="must_i_destroy_the_components_i_own?">Must I destroy the components I own?</a></h3>
<p>

That depends.  When a parent widget is destroyed, all child widgets
are destroyed automatically. Thus, if your object is a <b class='cmd'>snit::widget</b>
or <b class='cmd'>snit::widgetadaptor</b> you don't need to destroy any components
that are widgets, because they will generally be children or
descendants of your megawidget.

<p>

If your object is an instance of <b class='cmd'>snit::type</b>, though, none of its
owned components will be destroyed automatically, nor will be
non-widget components of a <b class='cmd'>snit::widget</b> be destroyed
automatically.  All such owned components must be destroyed
explicitly, or they won't be destroyed at all.

<h3><a name="can_i_expose_a_component's_object_command_as_part_of_my_interface?">Can I expose a component's object command as part of my interface?</a></h3>
<p>

Yes, and there are two ways to do it.  The most appropriate way is
usually to use <a href="#delegation">DELEGATION</a>.  Delegation allows you to pass
the options and methods you specify along to particular components.
This effectively hides the components from the users of your type, and
ensures good encapsulation.

<p>

However, there are times when it's appropriate, not to mention
simpler, just to make the entire component part of your type's public
interface.


<h3><a name="how_do_i_expose_a_component's_object_command?">How do I expose a component's object command?</a></h3>
<p>

When you declare the component, specify the <b class='cmd'>component</b>
statement's <strong>-public</strong> option.  The value of this option is the
name of a method which will be delegated to your component's object 
command.

<p>

For example, supposed you've written a combobox megawidget which owns
a listbox widget, and you want to make the listbox's entire interface 
public.  You can do it like this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget combobox {
     expose listbox -public listbox

     constructor {args} {
         install listbox using listbox $win.listbox ....
     }
}

combobox .mycombo
.mycombo listbox configure -width 30
</pre></td></tr></table></p>
<p>

Your comobox widget, <b class='cmd'>.mycombo</b>, now has a <strong>listbox</strong> method
which has all of the same subcommands as the listbox widget itself.
Thus, the above code sets the listbox component's width to 30.

<p>

Usually you'll let the method name be the same as the component name;
however, you can name it anything you like.

<h2><a name="type_components">TYPE COMPONENTS</a></h2>
<p>

<h3><a name="what_is_a_type_component?">What is a type component?</a></h3>
<p>

A type component is a component that belongs to the type itself
instead of to a particular instance of the type.  The relationship
between components and type components is the same as the
relationship between <a href="#instance_variables">INSTANCE VARIABLES</a> and 
<a href="#type_variables">TYPE VARIABLES</a>.  Both <a href="#instance_methods">INSTANCE METHODS</a> and
<a href="#type_methods">TYPE METHODS</a> can be delegated to type components.

<p>

Once you understand <a href="#components">COMPONENTS</a> and
<a href="#delegation">DELEGATION</a>, type components are just more of the same.

<h3><a name="how_do_i_declare_a_type_component?">How do I declare a type component?</a></h3>
<p>

Declare a type component using the <b class='cmd'>typecomponent</b> statement.  It
takes the same options (<strong>-inherit</strong> and <strong>-public</strong>) as the
<b class='cmd'>component</b> statement does, and defines a type variable to hold
the type component's object command.

<p>

Suppose in your model you've got many dogs, but only one
veterinarian.  You might make the veterinarian a type component.

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type veterinarian { ... }

snit::type dog {
    typecomponent vet

    # ...
}
</pre></td></tr></table></p>

<h3><a name="how_do_i_install_a_type_component?">How do I install a type component?</a></h3>
<p>

Just use the <b class='cmd'>set</b> command to assign the component's object
command to the type component.  Because types 
(even <b class='cmd'>snit::widget</b> types) are not widgets, and do not have
options anyway, the extra features of the <b class='cmd'>install</b> command are
not needed.

<p>

You'll usually install type components in the type constructor, as
shown here:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type veterinarian { ... }

snit::type dog {
    typecomponent vet

    typeconstructor {
        set vet [veterinarian %AUTO%]
    }
}
</pre></td></tr></table></p>

<h3><a name="are_there_any_limitations_on_type_component_names?">Are there any limitations on type component names?</a></h3>
<p>

Yes, the same as on <a href="#instance_variables">INSTANCE VARIABLES</a>, 
<a href="#type_variables">TYPE VARIABLES</a>, and normal <a href="#components">COMPONENTS</a>.


<h2><a name="delegation">DELEGATION</a></h2>
<p>

<h3><a name="what_is_delegation?">What is delegation?</a></h3>
<p>

Delegation, simply put, is when you pass a task you've been given to
one of your assistants.  (You do have assistants, don't you?)  Snit
objects can do the same thing.  The following example shows one way in
which the <b class='cmd'>dog</b> object can delegate its <b class='cmd'>wag</b> method and its
<strong>-taillength</strong> option to its <b class='cmd'>tail</b> component.

<p> 
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    variable mytail

    option -taillength  -configuremethod SetTailOption  -cgetmethod      GetTailOption
    

    method SetTailOption {option value} {
         $mytail configure $option $value
    }

    method GetTailOption {option} {
         $mytail cget $option
    }

    method wag {} {
        $mytail wag
    }

    constructor {args} {
        install mytail using tail %AUTO% -partof $self
        $self configurelist $args
    }

}
</pre></td></tr></table></p>
<p>

This is the hard way to do it, by it demonstrates what delegation is
all about.  See the following answers for the easy way to do it.

<p>

Note that the constructor calls the <strong>configurelist</strong> method
<b class='cmd'>after</b> it creates its <b class='cmd'>tail</b>; otherwise,
if <strong>-taillength</strong> appeared in the list of <strong>args</strong> we'd get an
error.

<h3><a name="how_can_i_delegate_a_method_to_a_component_object?">How can I delegate a method to a component object?</a></h3>
<p>

Delegation occurs frequently enough that Snit makes it easy. Any
method can be delegated to any component or type component 
by placing a single <b class='cmd'>delegate</b> statement in the type definition.

(See <a href="#components">COMPONENTS</a> and <a href="#type_components">TYPE COMPONENTS</a> for more
 information about component names.)

<p>

For example, here's a much better way to delegate the <b class='cmd'>dog</b>
object's <b class='cmd'>wag</b> method:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    delegate method wag to mytail

    constructor {args} {
        install mytail using tail %AUTO% -partof $self
        $self configurelist $args
    }
}
::dog
% snit::type tail {
    method wag {} { return &quot;Wag, wag, wag.&quot;}
}
::tail
% dog spot
::spot
% spot wag
Wag, wag, wag.
</pre></td></tr></table></p>
<p>

This code has the same effect as the code shown under the previous
question: when a <b class='cmd'>dog</b>'s <b class='cmd'>wag</b> method is called, the call and
its arguments are passed along automatically to the <b class='cmd'>tail</b> object.

<p>

Note that when a component is mentioned in a <b class='cmd'>delegate</b> statement,
the component's instance variable is defined implicitly.  However,
it's still good practice to declare it explicitly using the
<b class='cmd'>component</b> statement.

<p>

Note also that you can define a method name using the <b class='cmd'>method</b>
statement, or you can define it using <b class='cmd'>delegate</b>; you can't do
both.

<h3><a name="can_i_delegate_to_a_method_with_a_different_name?">Can I delegate to a method with a different name?</a></h3>
<p>

Suppose you wanted to delegate the <b class='cmd'>dog</b>'s <strong>wagtail</strong> method to
the <b class='cmd'>tail</b>'s <strong>wag</strong> method.  After all you wag the tail, not
the dog.  It's easily done:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate method wagtail to mytail as wag

    constructor {args} {
        install mytail using tail %AUTO% -partof $self
        $self configurelist $args
    }
}
</pre></td></tr></table></p>
<p>


<h3><a name="can_i_delegate_to_a_method_with_additional_arguments?">Can I delegate to a method with additional arguments?</a></h3>
<p>

Suppose the <b class='cmd'>tail</b>'s <strong>wag</strong> method takes as an argument the
number of times the tail should be wagged.  You want to delegate the
<b class='cmd'>dog</b>'s <strong>wagtail</strong> method to the <b class='cmd'>tail</b>'s <strong>wag</strong>
method, specifying that the tail should be wagged exactly three times.
This is easily done, too:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate method wagtail to mytail as {wag 3}
    # ...
}

snit::type tail {
    method wag {count} {
        return [string repeat &quot;Wag &quot; $count]
    }
    # ...
}
</pre></td></tr></table></p>
<p>

<h3><a name="can_i_delegate_a_method_to_something_other_than_an_object?">Can I delegate a method to something other than an object?</a></h3>
<p>

Normal method delegation assumes that you're delegating a method (a
subcommand of an object command) to a method of another object (a 
subcommand of a different object command).  But not all Tcl objects
follow Tk conventions, and not everything you'd to which you'd like
to delegate a method is necessary an object.  Consequently, Snit makes
it easy to delegate a method to pretty much anything you like using
the <b class='cmd'>delegate</b> statement's <strong>using</strong> clause.

<p>

Suppose your dog simulation stores dogs in a database, each dog as a
single record.  The database API you're using provides a number of
commands to manage records; each takes the record ID (a string you
choose) as its first argument.  For example, <b class='cmd'>saverec</b>
saves a record.  If you let the record ID be the name of the dog
object, you can delegate the dog's <strong>save</strong> method to the 
<b class='cmd'>saverec</b> command as follows:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate method save using {saverec %s}
}
</pre></td></tr></table></p>

The <strong>%s</strong> is replaced with the instance name when the 
<strong>save</strong> method is called; any additional arguments are the
appended to the resulting command.

<p>

The <strong>using</strong> clause understands a number of other %-conversions;
in addition to the instance name, you can substitute in the method 
name (<strong>%m</strong>), the type name (<strong>%t</strong>), the instance 
namespace (<strong>%n</strong>), the Tk window name (<strong>%w</strong>), and,
if a component or typecomponent name was given in the 
<b class='cmd'>delegate</b> statement, the component's object command 
(<strong>%c</strong>).

<h3><a name="how_can_i_delegate_a_method_to_a_type_component_object?">How can I delegate a method to a type component object?</a></h3>
<p>

Just exactly as you would to a component object.  The 
<b class='cmd'>delegate method</b> statement accepts both component and type
component names in its <strong>to</strong> clause.

<h3><a name="how_can_i_delegate_a_type_method_to_a_type_component_object?">How can I delegate a type method to a type component object?</a></h3>
<p>

Use the <b class='cmd'>delegate typemethod</b> statement.  It works like
<b class='cmd'>delegate method</b>, with these differences: first, it defines
a type method instead of an instance method; second, the 
<strong>using</strong> clause ignores the <strong>%s</strong>, <strong>%n</strong>, 
and <strong>%w</strong> %-conversions.

<p>

Naturally, you can't delegate a type method to an instance
component...Snit wouldn't know which instance should receive it.

<h3><a name="how_can_i_delegate_an_option_to_a_component_object?">How can I delegate an option to a component object?</a></h3>
<p>

The first question in this section (see <a href="#delegation">DELEGATION</a>) shows
one way to delegate an option to a component; but this pattern occurs
often enough that Snit makes it easy.  For example, every <b class='cmd'>tail</b>
object has a <strong>-length</strong> option; we want to allow the creator of
a <b class='cmd'>dog</b> object to set the tail's length.  We can do this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::type dog {
    delegate option -length to mytail

    constructor {args} {
        install mytail using tail %AUTO% -partof $self
        $self configurelist $args
    }
}
::dog
% snit::type tail {
    option -partof
    option -length 5
}
::tail
% dog spot -length 7
::spot
% spot cget -length
7
</pre></td></tr></table></p>
<p>

This produces nearly the same result as the <strong>-configuremethod</strong> and
<strong>-cgetmethod</strong> shown under the first question in this
section: whenever a <b class='cmd'>dog</b> object's <strong>-length</strong> option is set
or retrieved, the underlying <b class='cmd'>tail</b> object's option is set or
retrieved in turn.

<p>

Note that you can define an option name using the <b class='cmd'>option</b>
statement, or you can define it using <b class='cmd'>delegate</b>; you can't do
both.

<h3><a name="can_i_delegate_to_an_option_with_a_different_name?">Can I delegate to an option with a different name?</a></h3>
<p>

In the previous answer we delegated the <b class='cmd'>dog</b>'s <strong>-length</strong>
option down to its <b class='cmd'>tail</b>.  This is, of course, wrong.  The dog
has a length, and the tail has a length, and they are different.  What
we'd really like to do is give the <b class='cmd'>dog</b> a <strong>-taillength</strong>
option, but delegate it to the <b class='cmd'>tail</b>'s <strong>-length</strong> option:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate option -taillength to mytail as -length

    constructor {args} {
        set mytail [tail %AUTO% -partof $self]
        $self configurelist $args
    }
}
</pre></td></tr></table></p>
<p>

<h3><a name="how_can_i_delegate_any_unrecognized_method_or_option_to_a_component_object?">How can I delegate any unrecognized method or option to a component object?</a></h3>
<p>

It may happen that a Snit object gets most of its behavior from one of
its components.  This often happens with <b class='cmd'>snit::widgetadaptors</b>,
for example, where we wish to slightly the modify the behavior of an
existing widget.  To carry on with our <b class='cmd'>dog</b> example, however, suppose
that we have a <b class='cmd'>snit::type</b> called <b class='cmd'>animal</b> that implements a
variety of animal behaviors--moving, eating, sleeping, and so forth.

We want our <b class='cmd'>dog</b> objects to inherit these same behaviors, while
adding dog-like behaviors of its own.

Here's how we can give a <b class='cmd'>dog</b> methods and options of its own
while delegating all other methods and options to its <b class='cmd'>animal</b>
component:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate option * to animal
    delegate method * to animal

    option -akc 0

    constructor {args} {
        install animal using animal %AUTO% -name $self
        $self configurelist $args
    }

    method wag {} {
        return &quot;$self wags its tail&quot;
    }
}
</pre></td></tr></table></p>
<p>

That's it.  A <b class='cmd'>dog</b> is now an <b class='cmd'>animal</b> that has a
<strong>-akc</strong> option and can <b class='cmd'>wag</b> its tail.

<p>

Note that we don't need to specify the full list of method names or
option names that <b class='cmd'>animal</b> will receive.
It gets anything <b class='cmd'>dog</b> doesn't recognize--and if it doesn't
recognize it either, it will simply throw an error, just as it should.

<p>

You can also delegate all unknown type methods to a type component
using <b class='cmd'>delegate typemethod *</b>.

<h3><a name="how_can_i_delegate_all_but_certain_methods_or_options_to_a_component?">How can I delegate all but certain methods or options to a component?</a></h3>
<p>

In the previous answer, we said that every <b class='cmd'>dog</b> is
an <b class='cmd'>animal</b> by delegating all unknown methods and options to the
<strong>animal</strong> component. But what if the <b class='cmd'>animal</b> type has some
methods or options that we'd like to suppress?

<p>

One solution is to explicitly delegate all the options and methods,
and forgo the convenience of <b class='cmd'>delegate method *</b> and
<b class='cmd'>delegate option *</b>.  But if we wish to suppress only a few
options or methods, there's an easier way:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    delegate option * to animal except -numlegs
    delegate method * to animal except {fly climb}

    # ...

    constructor {args} {
        install animal using animal %AUTO% -name $self -numlegs 4
        $self configurelist $args
    }

    # ...
}
</pre></td></tr></table></p>
<p>

Dogs have four legs, so we specify that explicitly when we create the
<strong>animal</strong> component, and explicitly exclude <strong>-numlegs</strong> from the
set of delegated options.  Similarly, dogs can neither 
<strong>fly</strong> nor <strong>climb</strong>,
so we exclude those <b class='cmd'>animal</b> methods as shown.

<h2><a name="widgets">WIDGETS</a></h2>
<p>

<h3><a name="what_is_a_snit::widget?">What is a snit::widget?</a></h3>
<p>

A <b class='cmd'>snit::widget</b> is the Snit version of what Tcl programmers
usually call a <em>megawidget</em>: a widget-like object usually
consisting of one or more Tk widgets all contained within a Tk frame.

<p>

A <b class='cmd'>snit::widget</b> is also a special kind of <b class='cmd'>snit::type</b>.  Just
about everything in this FAQ list that relates to <b class='cmd'>snit::types</b>
also applies to <b class='cmd'>snit::widgets</b>.


<h3><a name="how_do_i_define_a_snit::widget?">How do I define a snit::widget?</a></h3>
<p>

<b class='cmd'>snit::widgets</b> are defined using the <b class='cmd'>snit::widget</b> command,
just as <b class='cmd'>snit::types</b> are defined by the <b class='cmd'>snit::type</b> command.

<p>

The body of the definition can contain all of the same kinds of
statements, plus a couple of others which will be mentioned below.


<h3><a name="how_do_snit::widgets_differ_from_snit::types?">How do snit::widgets differ from snit::types?</a></h3>
<p>

<ul>
<li>

The name of an instance of a <b class='cmd'>snit::type</b> can be any valid Tcl
command name, in any namespace.

The name of an instance of a <b class='cmd'>snit::widget</b> must be a valid Tk
widget name, and its parent widget must already exist.


<br><br>
<li>

An instance of a <b class='cmd'>snit::type</b> can be destroyed by calling

its <b class='cmd'>destroy</b> method.  Instances of a <b class='cmd'>snit::widget</b> have no
destroy method; use the Tk <b class='cmd'>destroy</b> command instead.


<br><br>
<li>

Every instance of a <b class='cmd'>snit::widget</b> has one predefined component
called its <strong>hull</strong> component.

The hull is a Tk <b class='cmd'>frame</b> or <b class='cmd'>toplevel</b> widget; any other
widgets created as part of the <b class='cmd'>snit::widget</b> will usually be
contained within this frame.

<br><br>
<li>

<b class='cmd'>snit::widget</b>s can have their options receive default values from
<a href="#the_tk_option_database">THE TK OPTION DATABASE</a>.

</ul>



<h3><a name="what_is_a_hull_component?">What is a hull component?</a></h3>
<p>

Snit can't create a Tk widget object; only Tk can do that.

Thus, every instance of a <b class='cmd'>snit::widget</b> must be wrapped around a
genuine Tk widget; this Tk widget is called the <em>hull component</em>.

Snit effectively piggybacks the behavior you define (methods, options,
and so forth) on top of the hull component so that the whole thing
behaves like a standard Tk widget.

<p>

For <b class='cmd'>snit::widget</b>s the hull component must be a Tk <b class='cmd'>frame</b> or
<b class='cmd'>toplevel</b> widget; any other widgets created as part of
the <b class='cmd'>snit::widget</b> will be contained within this frame or
toplevel.

<p>

<b class='cmd'>snit::widgetadaptor</b>s differ from <b class='cmd'>snit::widget</b>s chiefly in
that any kind of widget can be used as the hull component; see
<a href="#widget_adaptors">WIDGET ADAPTORS</a>.



<h3><a name="how_can_i_set_the_hull_type_for_a_snit::widget?">How can I set the hull type for a snit::widget?</a></h3>
<p>

A <b class='cmd'>snit::widget</b>'s hull component will usually be a Tk <b class='cmd'>frame</b>
widget; however, it may also be a <b class='cmd'>toplevel</b> widget.  You can
explicitly choose one or the other by including the <b class='cmd'>hulltype</b>
command in the widget definition:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mytoplevel {
    hulltype toplevel

    # ...
}
</pre></td></tr></table></p>
<p>

If no <b class='cmd'>hulltype</b> command appears, the hull will be a <b class='cmd'>frame</b>.



<h3><a name="how_should_i_name_widgets_which_are_components_of_a_snit::widget?">How should I name widgets which are components of a snit::widget?</a></h3>
<p>

Every widget, whether a genuine Tk widget or a Snit megawidget, has to
have a valid Tk window name.  When a <b class='cmd'>snit::widget</b> is first
created, its instance name, <strong>self</strong>, is a Tk window name;

however, if the <b class='cmd'>snit::widget</b> is used as the hull component by a
<b class='cmd'>snit::widgetadaptor</b> its instance name will be changed to
something else.  For this reason, every <b class='cmd'>snit::widget</b> method,
constructor, destructor, and so forth is passed another implicit
argument, <strong>win</strong>, which is the window name of the megawidget.  Any
children should be named using <strong>win</strong> as the root.

<p>

Thus, suppose you're writing a toolbar widget, a frame consisting of a
number of buttons placed side-by-side.  It might look something like
this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget toolbar {
    delegate option * to hull

    constructor {args} {
        button $win.open -text Open -command [mymethod open]
        button $win.save -text Save -command [mymethod save]

        # ....

        $self configurelist $args

    }
}
</pre></td></tr></table></p>
<p>

See also the question on renaming objects, toward the top of this
file.

<h2><a name="widget_adaptors">WIDGET ADAPTORS</a></h2>
<p>

<h3><a name="what_is_a_snit::widgetadaptor?">What is a snit::widgetadaptor?</a></h3>
<p>

A <b class='cmd'>snit::widgetadaptor</b> is a kind of <b class='cmd'>snit::widget</b>.  Whereas
a <b class='cmd'>snit::widget</b>'s hull is automatically created and is always a
Tk frame, a <b class='cmd'>snit::widgetadaptor</b> can be based on any Tk
widget--or on any Snit megawidget, or even (with luck) on megawidgets
defined using some other package.

<p>

It's called a <em>widget adaptor</em> because it allows you to take an
existing widget and customize its behavior.


<h3><a name="how_do_i_define_a_snit::widgetadaptor?">How do I define a snit::widgetadaptor?</a></h3>
<p>

Use the <b class='cmd'>snit::widgetadaptor</b> command.  The definition for a
<b class='cmd'>snit::widgetadaptor</b> looks just like that for a <b class='cmd'>snit::type</b>
or <b class='cmd'>snit::widget</b>, except that the constructor must create and
install the hull component.

<p>

For example, the following code creates a read-only text widget by the
simple device of turning its <strong>insert</strong> and <strong>delete</strong>
methods into no-ops.  Then, we define new methods, <strong>ins</strong> and
<strong>del</strong>,

which get delegated to the hull component as <strong>insert</strong> and
<strong>delete</strong>.  Thus, we've adapted the text widget and given it new
behavior while still leaving it fundamentally a text widget.

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>::snit::widgetadaptor rotext {

    constructor {args} {
        # Create the text widget; turn off its insert cursor
        installhull using text -insertwidth 0

        # Apply any options passed at creation time.
        $self configurelist $args
    }

    # Disable the text widget's insert and delete methods, to
    # make this readonly.
    method insert {args} {}
    method delete {args} {}

    # Enable ins and del as synonyms, so the program can insert and
    # delete.
    delegate method ins to hull as insert
    delegate method del to hull as delete
    
    # Pass all other methods and options to the real text widget, so
    # that the remaining behavior is as expected.
    delegate method * to hull
    delegate option * to hull
}
</pre></td></tr></table></p>
<p>

The most important part is in the constructor.
Whereas <b class='cmd'>snit::widget</b> creates the hull for you,
<b class='cmd'>snit::widgetadaptor</b> cannot -- it doesn't know what kind of
widget you want.  So the first thing the constructor does is create
the hull component (a Tk text widget in this case), and then installs
it using the <b class='cmd'>installhull</b> command.

<p>

<em>Note:</em> There is no instance command until you create one by
installing a hull component.  Any attempt to pass methods to <strong>$self</strong>
prior to calling <b class='cmd'>installhull</b> will fail.

<h3><a name="can_i_adapt_a_widget_created_elsewhere_in_the_program?">Can I adapt a widget created elsewhere in the program?</a></h3>
<p>

Yes.

<p>

At times, it can be convenient to adapt a pre-existing widget instead
of creating your own.
For example, the Bwidget <b class='cmd'>PagesManager</b> widget manages a
set of <b class='cmd'>frame</b> widgets, only one of which is visible at a time.
The application chooses which <b class='cmd'>frame</b> is visible.  All of the
These <b class='cmd'>frame</b>s are created by the <b class='cmd'>PagesManager</b> itself, using
its <strong>add</strong> method.  It's convenient to adapt these frames to
do what we'd like them to do.

<p>

In a case like this, the Tk widget will already exist when the
<b class='cmd'>snit::widgetadaptor</b> is created.  Snit provides an alternate form
of the <b class='cmd'>installhull</b> command for this purpose:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widgetadaptor pageadaptor {
    constructor {args} {
        # The widget already exists; just install it.
        installhull $win

        # ...
    }
}
</pre></td></tr></table></p>

<h3><a name="can_i_adapt_another_megawidget?">Can I adapt another megawidget?</a></h3>
<p>

Maybe. If the other megawidget is a <b class='cmd'>snit::widget</b> or
<b class='cmd'>snit::widgetadaptor</b>, then yes.  If it isn't then, again, maybe.
You'll have to try it and see.  You're most likely to have trouble
with widget destruction--you have to make sure that your 
megawidget code receives the <strong>&lt;Destroy&gt;</strong> event before the
megawidget you're adapting does.

<h2><a name="the_tk_option_database">THE TK OPTION DATABASE</a></h2>
<p>

<h3><a name="what_is_the_tk_option_database?">What is the Tk option database?</a></h3>
<p>

The Tk option database is a database of default option values
maintained by Tk itself; every Tk application has one.  The concept of
the option database derives from something called the X Windows
resource database; however, the option database is available in every
Tk implementation, including those which do not use the X Windows
system (e.g., Microsoft Windows).

<p>

Full details about the Tk option database are beyond the scope of this
document; both <em>Practical Programming in Tcl and Tk</em> by Welch,
Jones, and Hobbs, and <em>Effective Tcl/Tk Programming</em> by
Harrison and McClennan., have good introductions to it.

<p>

Snit is implemented so that most of the time it will simply do the
right thing with respect to the option database, provided that the
widget developer does the right thing by Snit.  The body of this
section goes into great deal about what Snit requires.  The following
is a brief statement of the requirements, for reference.

<p>

<ul>

<li>

If the widget's default widget class is not what is desired, set it
explicitly using the <b class='cmd'>widgetclass</b> statement in the widget
definition.

<br><br>
<li>

When defining or delegating options, specify the resource and class
names explicitly when necessary.

<br><br>
<li>

Use the <b class='cmd'>installhull using</b> command to create and install the
hull for <b class='cmd'>snit::widgetadaptor</b>s.

<br><br>
<li>

Use the <b class='cmd'>install</b> command to create and install all
components which are widgets.

<br><br>
<li>

Use the <b class='cmd'>install</b> command to create and install 
components which aren't widgets if you'd like them to 
receive option values from the option database.

</ul>
<p>

The interaction of Tk widgets with the option database is a complex
thing; the interaction of Snit with the option database is even more
so, and repays attention to detail.


<h3><a name="do_snit::types_use_the_tk_option_database?">Do snit::types use the Tk option database?</a></h3>
<p>

No, they don't; querying the option database requires a Tk window
name, and <b class='cmd'>snit::type</b>s don't have one.

<p>

If you create an instance of a <b class='cmd'>snit::type</b> as a
component of a <b class='cmd'>snit::widget</b> or <b class='cmd'>snit::widgetadaptor</b>, on the
other hand, and if any options are delegated to the component,
and if you use <b class='cmd'>install</b> to create and install it, then
the megawidget will query the option database on the
<b class='cmd'>snit::type</b>'s behalf.  This might or might not be what you
want, so take care.

<h3><a name="what_is_my_snit::widget's_widget_class?">What is my snit::widget's widget class?</a></h3>
<p>

Every Tk widget has a &quot;widget class&quot;: a name that is used when adding
option settings to the database.  For Tk widgets, the widget class is
the same as the widget command name with an initial capital.  For
example, the widget class of the Tk <b class='cmd'>button</b> widget is 
<strong>Button</strong>.

<p>

Similarly, the widget class of a <b class='cmd'>snit::widget</b> defaults to the
unqualified type name with the first letter capitalized.  For example,
the widget class of

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget ::mylibrary::scrolledText { ... }
</pre></td></tr></table></p>
<p>

is <strong>ScrolledText</strong>.

<p>

The widget class can also be set explicitly using the
<b class='cmd'>widgetclass</b> statement within the <b class='cmd'>snit::widget</b> definition:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget ::mylibrary::scrolledText {
    widgetclass Text

    # ...
}
</pre></td></tr></table></p>
<p>

The above definition says that a <b class='cmd'>scrolledText</b> megawidget has the
same widget class as an ordinary <b class='cmd'>text</b> widget.  This might or
might not be a good idea, depending on how the rest of the megawidget
is defined, and how its options are delegated.

<h3><a name="what_is_my_snit::widgetadaptor's_widget_class?">What is my snit::widgetadaptor's widget class?</a></h3>
<p>

The widget class of a <b class='cmd'>snit::widgetadaptor</b> is just the widget
class of its hull widget; Snit has no control over this.

<p>

Note that the widget class can be changed only for <b class='cmd'>frame</b> and
<b class='cmd'>toplevel</b> widgets, which is why these are the valid hull types
for <b class='cmd'>snit::widget</b>s.

<p>

Try to use <b class='cmd'>snit::widgetadaptor</b>s only to make small modifications
to another widget's behavior.  Then, it will usually not make sense to
change the widget's widget class anyway.


<h3><a name="what_are_option_resource_and_class_names?">What are option resource and class names?</a></h3>
<p>

Every Tk widget option has three names: the option name, the resource
name, and the class name.

The option name begins with a hyphen and is all lowercase; it's used
when creating widgets, and with the <b class='cmd'>configure</b> and <b class='cmd'>cget</b>
commands.

<p>

The resource and class names are used to initialize option
default values by querying the option database.
The resource name is usually just the option
name minus the hyphen, but may contain uppercase letters at word
boundaries; the class name is usually just the resource
name with an initial capital, but not always.  For example, here are
the option, resource, and class names for several Tk <b class='cmd'>text</b>
widget options:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    -background         background         Background 
    -borderwidth        borderWidth        BorderWidth 
    -insertborderwidth  insertBorderWidth  BorderWidth 
    -padx               padX               Pad 
</pre></td></tr></table></p>
<p>

As is easily seen, sometimes the resource and class names can be
inferred from the option name, but not always.


<h3><a name="what_are_the_resource_and_class_names_for_my_megawidget's_options?">What are the resource and class names for my megawidget's options?</a></h3>
<p>

For options implicitly delegated to a component using
<b class='cmd'>delegate option *</b>, the resource and class names will be
exactly those defined by the component.  The <b class='cmd'>configure</b> method
returns these names, along with the option's default and current
values:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::widget mytext {
    delegate option * to text

    constructor {args} {
        install text using text .text
        # ...
    }

    # ...
}
::mytext
% mytext .text
.text
% .text configure -padx
-padx padX Pad 1 1
%
</pre></td></tr></table></p>
<p>

For all other options (whether locally defined or explicitly
delegated), the resource and class names can be defined explicitly, or
they can be allowed to have default values.

<p>

By default, the resource name is just the option name minus the
hyphen; the the class name is just the option name with an initial
capital letter.  For example, suppose we explicitly delegate &quot;-padx&quot;:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::widget mytext {
    option -myvalue 5

    delegate option -padx to text
    delegate option * to text

    constructor {args} {
        install text using text .text
        # ...
    }

    # ...
}
::mytext
% mytext .text
.text
% .text configure -myvalue
-myvalue myvalue Myvalue 5 5
% .text configure -padx
-padx padx Padx 1 1
%
</pre></td></tr></table></p>
<p>

Here the resource and class names are chosen using the default rules.
Often these rules are sufficient, but in the case of &quot;-padx&quot; we'd most
likely prefer that the option's resource and class names are the same
as for the built-in Tk widgets.  This is easily done:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>% snit::widget mytext {
    delegate option {-padx padX Pad} to text

    # ...
}
::mytext
% mytext .text
.text
% .text configure -padx
-padx padX Pad 1 1
%
</pre></td></tr></table></p>


<h3><a name="how_does_snit_initialize_my_megawidget's_locally-defined_options?">How does Snit initialize my megawidget's locally-defined options?</a></h3>
<p>

The option database is queried for each of the megawidget's
locally-defined options, using the option's resource and class name.
If the result isn't &quot;&quot;, then it replaces the default value given in
widget definition.  In either case, the default can be overridden by
the caller.  For example,

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>option add *Mywidget.texture pebbled

snit::widget mywidget {
    option -texture smooth
    # ...
}

mywidget .mywidget -texture greasy
</pre></td></tr></table></p>
<p>

Here, <strong>-texture</strong> would normally default to &quot;smooth&quot;, but because of
the entry added to the option database it defaults to &quot;pebbled&quot;.
However, the caller has explicitly overridden the default, and so the
new widget will be &quot;greasy&quot;.

<h3><a name="how_does_snit_initialize_delegated_options?">How does Snit initialize delegated options?</a></h3>
<p>

That depends on whether the options are delegated to the hull, or to
some other component.


<h3><a name="how_does_snit_initialize_options_delegated_to_the_hull?">How does Snit initialize options delegated to the hull?</a></h3>
<p>

A <b class='cmd'>snit::widget</b>'s hull is a widget, and given that its class has
been set it is expected to query the option database for itself.  The
only exception concerns options that are delegated to it with a
different name.  Consider the following code:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>option add *Mywidget.borderWidth 5
option add *Mywidget.relief sunken
option add *Mywidget.hullbackground red
option add *Mywidget.background green

snit::widget mywidget {
    delegate option -borderwidth to hull
    delegate option -hullbackground to hull as -background
    delegate option * to hull
    # ...
}

mywidget .mywidget

set A [.mywidget cget -relief]
set B [.mywidget cget -hullbackground]
set C [.mywidget cget -background]
set D [.mywidget cget -borderwidth]
</pre></td></tr></table></p>
<p>

The question is, what are the values of variables A, B, C and D?

<p>

The value of A is &quot;sunken&quot;.  The hull is a Tk frame which has been
given the widget class <strong>Mywidget</strong>; it will automatically query the
option database and pick up this value.  Since the <strong>-relief</strong> option is
implicitly delegated to the hull, Snit takes no action.

<p>

The value of B is &quot;red&quot;.  The hull will automatically pick up the
value &quot;green&quot; for its <strong>-background</strong> option, just as it picked up the
<strong>-relief</strong> value.  However, Snit knows that <strong>-hullbackground</strong>
is mapped to the hull's <strong>-background</strong> option; hence, it queries 
the option database for <strong>-hullbackground</strong> and gets &quot;red&quot; and 
updates the hull accordingly.

<p>

The value of C is also &quot;red&quot;, because <strong>-background</strong> is implicitly
delegated to the hull; thus, retrieving it is the same as retrieving
<strong>-hullbackground</strong>.  Note that this case is unusual; the 
<strong>-background</strong> option should probably have been excluded using the delegate
statement's <strong>except</strong> clause, or (more likely) delegated to some other
component.

<p>

The value of D is &quot;5&quot;, but not for the reason you think.  Note that as
it is defined above, the resource name for <strong>-borderwidth</strong> defaults to
<strong>borderwidth</strong>, whereas the option database entry is 
<strong>borderWidth</strong>, in
accordance with the standard Tk naming for this option.  As with
<strong>-relief</strong>, the hull picks up its own <strong>-borderwidth</strong>
option before Snit
does anything.  Because the option is delegated under its own name,
Snit assumes that the correct thing has happened, and doesn't worry
about it any further.  To avoid confusion, the 
<strong>-borderwidth</strong> option
should have been delegated like this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    delegate option {-borderwidth borderWidth BorderWidth} to hull
</pre></td></tr></table></p>
<p>

For <b class='cmd'>snit::widgetadaptor</b>s, the case is somewhat altered.  Widget
adaptors retain the widget class of their hull, and the hull is not
created automatically by Snit.  Instead, the <b class='cmd'>snit::widgetadaptor</b>
must call <b class='cmd'>installhull</b> in its constructor.  The normal way
to do this is as follows:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widgetadaptor mywidget {
    # ...
    constructor {args} {
        # ...
        installhull using text -foreground white
        # ...
    }
    # ...
}
</pre></td></tr></table></p>
<p>

In this case, the <b class='cmd'>installhull</b> command will create the hull using
a command like this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    set hull [text $win -foreground white]
</pre></td></tr></table></p>
<p>

The hull is a <b class='cmd'>text</b> widget, so its widget class is <strong>Text</strong>.  Just
as with <b class='cmd'>snit::widget</b> hulls, Snit assumes that it will pick up
all of its normal option values automatically, without help from Snit.
Options delegated from a different name are initialized from the
option database in the same way as described above.

<p>

In earlier versions of Snit, <b class='cmd'>snit::widgetadaptor</b>s were expected
to call <b class='cmd'>installhull</b> like this:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>    installhull [text $win -foreground white]
</pre></td></tr></table></p>
<p>

This form still works--but Snit will not query the option database as
described above.

<h3><a name="how_does_snit_initialize_options_delegated_to_other_components?">How does Snit initialize options delegated to other components?</a></h3>
<p>

For hull components, Snit assumes that Tk will do most of the work
automatically.  Non-hull components are somewhat more complicated, because
they are matched against the option database twice.

<p>

A component widget remains a widget still, and is therefore
initialized from the option database in the usual way.  A <b class='cmd'>text</b>
widget remains a <b class='cmd'>text</b> widget whether it is a component of a
megawidget or not, and will be created as such.

<p>

But then, the option database is queried for all options delegated to
the component, and the component is initialized accordingly--provided
that the <b class='cmd'>install</b> command is used to create it.

<p>

Before option database support was added to Snit, the usual way to
create a component was to simply create it in the constructor and
assign its command name to the component variable:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    delegate option -background to myComp

    constructor {args} {
        set myComp [text $win.text -foreground black]
    }
}
</pre></td></tr></table></p>
<p>

The drawback of this method is that Snit has no opportunity to
initialize the component properly.  Hence, the following approach is
now used:

<p>
<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    delegate option -background to myComp

    constructor {args} {
        install myComp using text $win.text -foreground black
    }
}
</pre></td></tr></table></p>
<p>

The <b class='cmd'>install</b> command does the following:

<p>
<ul>

<li>

Builds a list of the options explicitly included in the <b class='cmd'>install</b>
command--in this case, <strong>-foreground</strong>.

<br><br>
<li>

Queries the option database for all options delegated explicitly to
the named component.

<br><br>
<li>

Creates the component using the specified command, after inserting
into it a list of options and values read from the option database.
Thus, the explicitly included options (like <strong>-foreground</strong>) will 
override anything read from the option database.

<br><br>
<li>

If the widget definition implicitly delegated options to the component
using <b class='cmd'>delegate option *</b>, then Snit calls the newly created
component's <b class='cmd'>configure</b> method to receive a list of all of the
component's options.  From this Snit builds a list of options
implicitly delegated to the component which were not explicitly
included in the <b class='cmd'>install</b> command.  For all such options, Snit
queries the option database and configures the component accordingly.

</ul>

You don't really need to know all of this; just use <b class='cmd'>install</b> to
install your components, and Snit will try to do the right thing.

<h3><a name="what_happens_if_i_install_a_non-widget_as_a_component_of_widget?">What happens if I install a non-widget as a component of widget?</a></h3>
<p>

A <b class='cmd'>snit::type</b> never queries the option database.
However, a <b class='cmd'>snit::widget</b> can have non-widget components.  And if
options are delegated to those components, and if the <b class='cmd'>install</b>
command is used to install those components, then they will be
initialized from the option database just as widget components are.

<p>

However, when used within a megawidget, <b class='cmd'>install</b> assumes that the
created component uses a reasonably standard widget-like creation
syntax.  If it doesn't, don't use <b class='cmd'>install</b>.

<h2><a name="ensemble_commands">ENSEMBLE COMMANDS</a></h2>
<p>

<h3><a name="what_is_an_ensemble_command?">What is an ensemble command?</a></h3>
<p>

An ensemble command is a command with subcommands.  Snit objects are
all ensemble commands; however, the term more usually refers to
commands like the standard Tcl commands <b class='cmd'>string</b>, <b class='cmd'>file</b>, 
and <b class='cmd'>clock</b>.  In a sense, these are singleton objects--there's
only one instance of them.

<h3><a name="how_can_i_create_an_ensemble_command_using_snit?">How can I create an ensemble command using Snit?</a></h3>
<p>

There are two ways--as a <b class='cmd'>snit::type</b>, or as an instance of
a <b class='cmd'>snit::type</b>.

<h3><a name="how_can_i_create_an_ensemble_command_using_an_instance_of_a_snit::type?">How can I create an ensemble command using an instance of a snit::type?</a></h3>
<p>

Define a type whose <a href="#instance_methods">INSTANCE METHODS</a> are the subcommands
of your ensemble command.  Then, create an instance of the type with
the desired name.

<p>

For example, the following code uses <a href="#delegation">DELEGATION</a> to create
a work-alike for the standard <b class='cmd'>string</b> command:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type ::mynamespace::mystringtype {
    delegate method * to stringhandler

    constructor {} {
        set stringhandler string
    }
}

::mynamespace::mystringtype mystring
</pre></td></tr></table></p>

We create the type in a namespace, so that the type command is hidden;
then we create a single instance with the desired name--
<b class='cmd'>mystring</b>, in this case.

<p>

This method has two drawbacks.  First, it leaves the type command
floating about.  More seriously, your shiny new ensemble
command will have <strong>cget</strong>, <strong>configure</strong>, <strong>info</strong>,
and <strong>destroy</strong> subcommands that you probably have no use for.
But read on.

<h3><a name="how_can_i_create_an_ensemble_command_using_a_snit::type?">How can I create an ensemble command using a snit::type?</a></h3>
<p>

Define a type whose <a href="#type_methods">TYPE METHODS</a> are the subcommands
of your ensemble command.<p>

For example, the following code uses <a href="#delegation">DELEGATION</a> to create
a work-alike for the standard <b class='cmd'>string</b> command:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mystring {
    delegate typemethod * to stringhandler

    typeconstructor {
        set stringhandler string
    }
}
</pre></td></tr></table></p>

Now the type command itself is your ensemble command.

<p>

This method has only one drawback, and though it's major, it's 
also surmountable.  Your new ensemble command will have
<strong>create</strong>, <strong>info</strong> and <strong>destroy</strong> subcommands
you don't want.  And worse yet, since the <strong>create</strong> method
can be implicit, users of your command will accidentally be creating
instances of your <b class='cmd'>mystring</b> type if they should mispell one
of the subcommands.  The command will succeed--the first time--but
won't do what's wanted.  This is very bad.

<p>

The work around is to set some <a href="#pragmas">PRAGMAS</a>, as shown here:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type mystring {
    pragma -hastypeinfo    no 
    pragma -hastypedestroy no 
    pragma -hasinstances   no

    delegate typemethod * to stringhandler

    typeconstructor {
        set stringhandler string
    }
}
</pre></td></tr></table></p>

Here we've used the <b class='cmd'>pragma</b> statement to tell Snit that we don't
want the <strong>info</strong> typemethod or the <strong>destroy</strong> typemethod,
and that our type has no instances; this eliminates the 
<strong>create</strong> typemethod and all related code.  As
a result, our ensemble command will be well-behaved, with no
unexpected subcommands.

<h2><a name="pragmas">PRAGMAS</a></h2>
<p>

<h3><a name="what_is_a_pragma?">What is a pragma?</a></h3>
<p>

A pragma is an option you can set in your type definitions that
affects how the type is defined and how it works once it is defined.

<h3><a name="how_do_i_set_a_pragma?">How do I set a pragma?</a></h3>
<p>

Use the <b class='cmd'>pragma</b> statement.  Each pragma is an option with a
value; each time you use the <b class='cmd'>pragma</b> statement you can set one or
more of them.

<h3><a name="how_can_i_get_rid_of_the_&quot;info&quot;_type_method?">How can I get rid of the &quot;info&quot; type method?</a></h3>
<p>

Set the <strong>-hastypeinfo</strong> pragma to <strong>no</strong>:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    pragma -hastypeinfo no
    # ...
}
</pre></td></tr></table></p>

Snit will refrain from defining the <strong>info</strong> type method.

<h3><a name="how_can_i_get_rid_of_the_&quot;destroy&quot;_type_method?">How can I get rid of the &quot;destroy&quot; type method?</a></h3>
<p>

Set the <strong>-hastypedestroy</strong> pragma to <strong>no</strong>:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    pragma -hastypedestroy no
    # ...
}
</pre></td></tr></table></p>

Snit will refrain from defining the <strong>destroy</strong> type method.

<h3><a name="how_can_i_get_rid_of_the_&quot;create&quot;_type_method?">How can I get rid of the &quot;create&quot; type method?</a></h3>
<p>

Set the <strong>-hasinstances</strong> pragma to <strong>no</strong>:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    pragma -hasinstances no
    # ...
}
</pre></td></tr></table></p>

Snit will refrain from defining the <strong>create</strong> type method;
if you call the type command with an unknown method name, you'll get
an error instead of a new instance of the type.

<p>

This is useful if you wish to use a <b class='cmd'>snit::type</b> to define
an ensemble command rather than a type with instances.

<p>

Pragmas <strong>-hastypemethods</strong> and <strong>-hasinstances</strong> cannot
both be false (or there'd be nothing left).

<h3><a name="how_can_i_get_rid_of_type_methods_altogether?">How can I get rid of type methods altogether?</a></h3>
<p>

Normal Tk widget type commands don't have subcommands; all they do is 
create widgets--in Snit terms, the type command calls the 
<strong>create</strong> type method directly.  To get the same behavior from 
Snit, set the <strong>-hastypemethods</strong> pragma to <strong>no</strong>:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    pragma -hastypemethods no
    #...
}

# Creates ::spot
dog spot

# Tries to create an instance called ::create
dog create spot
</pre></td></tr></table></p>

Pragmas <strong>-hastypemethods</strong> and <strong>-hasinstances</strong> cannot
both be false (or there'd be nothing left).

<h3><a name="why_can't_i_create_an_object_that_replaces_an_old_object_with_the_same_name?">Why can't I create an object that replaces an old object with the same name?</a></h3>
<p>

Up until Snit 0.95, you could use any name for an instance of a 
<b class='cmd'>snit::type</b>, even if the name was already in use by some other
object or command.  You could do the following, for example:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog { ... }

dog proc
</pre></td></tr></table></p>

You now have a new dog named &quot;proc&quot;, which is probably not something
that you really wanted to do.  As a result, Snit now throws an error
if your chosen instance name names an existing command.  To restore
the old behavior, set the <strong>-canreplace</strong> pragma to <strong>yes</strong>:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {
    pragma -canreplace yes
    # ...
}
</pre></td></tr></table></p>

<h3><a name="how_can_i_make_my_simple_type_run_faster?">How can I make my simple type run faster?</a></h3>
<p>

Set the <strong>-simpledispatch</strong> pragma to <strong>yes</strong>.

<p>

Snit method dispatch is both flexible and fast, but the flexibility
comes with a price.  If your type doesn't require the flexibility, the 
<strong>-simpledispatch</strong> pragma allows you to substitute a simpler
dispatch mechanism that runs quite a bit faster.  The limitations
are these:

<ul>

<li> Methods cannot be delegated.
<br><br>
<li> <b class='cmd'>uplevel</b> and <b class='cmd'>upvar</b> do not work as expected: the
caller's scope is two levels up rather than one.
<br><br>
<li> The option-handling methods 
(<b class='cmd'>cget</b>, <b class='cmd'>configure</b>, and <b class='cmd'>configurelist</b>) are very
slightly slower.
</ul>

<h2><a name="macros">MACROS</a></h2>
<p>

<h3><a name="what_is_a_macro?">What is a macro?</a></h3>
<p>

A Snit macro is nothing more than a Tcl proc that's defined in the 
Tcl interpreter used to compile Snit type definitions.

<h3><a name="what_are_macros_good_for?">What are macros good for?</a></h3>
<p>

You can use Snit macros to define new type definition syntax, and to
support conditional compilation.

<h3><a name="how_do_i_do_conditional_compilation?">How do I do conditional compilation?</a></h3>
<p>

Suppose you want your type to use a fast C extension if it's
available; otherwise, you'll fallback to a slower Tcl implementation.
You want to define one set of methods in the first case, and another
set in the second case.  But how can your type definition know whether
the fast C extension is available or not?

<p>

It's easily done.  Outside of any type definition, define a macro that
returns 1 if the extension is available, and 0 otherwise:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>if {$gotFastExtension} {
    snit::macro fastcode {} {return 1}
} else {
    snit::macro fastcode {} {return 0}
}
</pre></td></tr></table></p>

Then, use your macro in your type definition:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::type dog {

    if {[fastcode]} {
        # Fast methods
        method bark {} {...}
        method wagtail {} {...}
    } else {
        # Slow methods
        method bark {} {...}
        method wagtail {} {...}
    }
}
</pre></td></tr></table></p>

<h3><a name="how_do_i_define_new_type_definition_syntax?">How do I define new type definition syntax?</a></h3>
<p>

Use a macro.  For example, your <b class='cmd'>snit::widget</b>'s 
<strong>-background</strong> option should be propagated to a number
of component widgets.  You could implement that like this:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    option -background -default white -configuremethod PropagateBackground

    method PropagateBackground {option value} {
        $comp1 configure $option $value
        $comp2 configure $option $value
        $comp3 configure $option $value
    }
}
</pre></td></tr></table></p>

For one option, this is fine; if you've got a number of options, it
becomes tedious and error prone.  So package it as a macro:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::macro propagate {option &quot;to&quot; components} {
    option $option -configuremethod Propagate$option

    set body &quot;\n&quot;

    foreach comp $components {
        append body &quot;\$$comp configure $option \$value\n&quot;
    }

    method Propagate$option {option value} $body
}
</pre></td></tr></table></p>

Then you can use it like this:

<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::widget mywidget {
    option -background default -white
    option -foreground default -black

    propagate -background to {comp1 comp2 comp3}
    propagate -foreground to {comp1 comp2 comp3}
}
</pre></td></tr></table></p>

<h3><a name="are_there_are_restrictions_on_macro_names?">Are there are restrictions on macro names?</a></h3>
<p>

Yes, there are.  You can't redefine any standard Tcl commands or Snit
type definition statements.  You can use any other command name,
including the name of a previously defined macro.

<p>

If you're using Snit macros in your application, go ahead and name
them in the global namespace, as shown above.  But if you're using
them to define types or widgets for use by others, you should define
your macros in the same namespace as your types or widgets.  That way,
they won't conflict with other people's macros.

<p>

If my fancy <b class='cmd'>snit::widget</b> is called <b class='cmd'>::mylib::mywidget</b>, 
for example, then I should define my <b class='cmd'>propagate</b> macro as
<b class='cmd'>::mylib::propagate</b>:


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>snit::macro mylib::propagate {option &quot;to&quot; components} { ... }

snit::widget ::mylib::mywidget {
    option -background default -white
    option -foreground default -black

    mylib::propagate -background to {comp1 comp2 comp3}
    mylib::propagate -foreground to {comp1 comp2 comp3}
}
</pre></td></tr></table></p>





<h2><a name="keywords">KEYWORDS</a></h2>
<p>
BWidget, C++, Incr Tcl, adaptors, class, mega widget, object, object oriented, widget, widget adaptors
<h2><a name="copyright">COPYRIGHT</a></h2>
<p>
Copyright &copy; 2003-2005, by William H. Duquette<br>
</body></html>
