#
# PictureTypeD.itcl
#
# This class defines the process necessary to build a BRL-CAD
# picture of Type "D". This is a full-color image mixed with a
# line drawing. This image type differs from Type C in that the
# objects rendered in the line drawing may differ from those 
# rendered in the full-color image.
#


#
# Required packages
#
package require Itcl
package require Itk
package require Iwidgets 3.0

namespace eval RtWizard {
  
    package require PictureTypeBase 1.0
    package provide PictureTypeD 1.0

    #
    # Required packages
    #

    # None!

    #
    # The PictureTypeD class supports making a mixed full-color and line
    # image.
    #
    ::itcl::class PictureTypeD {

	inherit ::RtWizard::PictureTypeBase
	public {
	    constructor {args} {
		PictureTypeBase::constructor $args
	    } {}

	    #
	    # Methods that impliment abstract base class methods.
	    # Unfortunately, I have to redeclare them here. 
	    #
	    method activate {}
	    method deactivate {}
	    method preview {}
	    method fullSize {}
	}

	private {
	    method makeImage {fb w h}

	    variable fp
	    variable lp
	}
    }

    #--------------------#
    #   Public Methods   #
    #--------------------#    
    #
    # activate - called when this picture type is selected.
    #
    itcl::body PictureTypeD::activate { } {
	#
	# Open a new steps menu
	#
	$::wizardInstance openSteps

	#
	# Update the introductory text
	#
	$::introInstance configure -greeting "$introText"

	#
	# Activate the pages that are necessary for this picture
	# type.
	#
	set fp [$::wizardInstance enable fullColor]
	set lp [$::wizardInstance enable lines]
	[$fp component useMe] select
    }

    #
    # deactivate - called when this picture type is deselected.
    #
    itcl::body PictureTypeD::deactivate { } {
	#
	# Deactivate the pages.
	#
	$::wizardInstance disable fullColor
	$::wizardInstance disable lines

	#
	# Close the current steps menu
	#
	$::wizardInstance closeSteps
    }

    #
    # preview - generates a 1/4 size preview image, limited to on-screen
    #
    itcl::body PictureTypeD::preview { } {
	#
	# get height and width
	#
	set w [$::fbp getWidth]
	set w2 [ expr $w / 2 ]

	set h [$::fbp getHeight]
	set h2 [ expr $h / 2 ]

	#	
	# get a framebuffer
	#
	set fb [$::fbp getFrameBuffer $w2 $h2 true]

	#
	# get occlusion mode
	#
	set occMode [$lp getOcclusionMode]

	#
	# Run rt
	#
	rtCmd $fp $fb $w2 $h2

	#
	# Run RtEdge
	#
	rtedgeCmd $lp $fb $w2 $h2 1 $occMode $fp
    }    
    
    #
    # fullSize - generates a fullSize picture.
    #
    itcl::body PictureTypeD::fullSize { } {
	#
	# get height and width
	#
	set w [$::fbp getWidth]
	set h [$::fbp getHeight]

	#	
	# get a framebuffer
	#
	set fb [$::fbp getFrameBuffer $w $h]

	#
	# get occlusion mode
	#
	set occMode [$lp getOcclusionMode]

	#
	# Run rt
	#
	rtCmd $fp $fb $w $h

	#
	# Run RtEdge
	#
	rtedgeCmd $lp $fb $w $h 1 $occMode $fp
    }
}; # end namespace
















