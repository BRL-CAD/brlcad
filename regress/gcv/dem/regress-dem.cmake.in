# Values set at CMake configure time
set(CBDIR "@CMAKE_CURRENT_BINARY_DIR@")
set(CSDIR "@CMAKE_CURRENT_SOURCE_DIR@")
set(LOGFILE "${CBDIR}/regress-dem.log")

file(WRITE "${LOGFILE}" "Starting DEM conversion run\n")

# The executable locations aren't know at CMake configure time, so one of them
# is passed in via the EXEC variable at runtime.  De-quote it and assign it to
# the appropriate variable.
string(REPLACE "\\" "" GCV_EXEC "${EXEC}")
if (NOT EXISTS "${GCV_EXEC}")
  file(WRITE "${LOGFILE}" "gcv not found at location \"${GCV_EXEC}\" - aborting\n")
  message(FATAL_ERROR "Unable to find gcv, aborting.\nSee ${LOGFILE} for more details.")
endif (NOT EXISTS "${GCV_EXEC}")

# Clean up in case we've run before
set(OUTPUT_FILES
  "${CBDIR}/hawaii-c"
  "${CBDIR}/hawaii-c.g"
  )
foreach(of ${OUTPUT_FILES})
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${of}")
endforeach(of ${OUTPUT_FILES})

# Unpack
execute_process(COMMAND "@CMAKE_COMMAND@" -E tar xjf "${CSDIR}/hawaii-c.tar.bz2" WORKING_DIRECTORY "${CBDIR}")

# Run the converter
execute_process(
  COMMAND "${GCV_EXEC}" --input-format gdal "${CBDIR}/hawaii-c" "${CBDIR}/hawaii-c.g"
  RESULT_VARIABLE dem_g_result OUTPUT_VARIABLE dem_g_log ERROR_VARIABLE dem_g_log
  )
file(APPEND "${CBDIR}/regress-dem.log" "${dem_g_log}")
set(dem_g_log)
if(NOT EXISTS "${CBDIR}/hawaii-c.g")
  file(APPEND "${LOGFILE}" "Failure: ${dem_g_result}")
  message(FATAL_ERROR "Unable to convert ${CBDIR}/hawaii-c to ${CBDIR}/hawaii-c.g with ${GCV_EXEC}, aborting.\nSee ${LOGFILE} for more details.")
endif(NOT EXISTS "${CBDIR}/hawaii-c.g")

# The MD5 sum is too senstivie - check the file size to make sure
# we got a reasonable output.  This doesn't check the correctness
# of the data - haven't yet come up with a good answer for that
set(EXPECTED_SIZE 2965640)

# get output file size.  NOTE: - once we can require a new enough
# CMake (3.14+) use file(SIZE) for this...  in the meantime use
# the trick from https://stackoverflow.com/a/30010569
file(READ "${CBDIR}/hawaii-c.g" gdata HEX)
string(LENGTH "${gdata}" gsize)
math(EXPR gsize "${gsize} / 2")
file(APPEND "${LOGFILE}" "hawaii-c.g size: ${gsize}")

# See how big the size difference is
if ("${gsize}" GREATER "${EXPECTED_SIZE}")
  math(EXPR size_delta "${gsize} - ${EXPECTED_SIZE}")
else ("${gsize}" GREATER "${EXPECTED_SIZE}")
  math(EXPR size_delta "${EXPECTED_SIZE} - ${gsize}")
endif ("${gsize}" GREATER "${EXPECTED_SIZE}")

# Define a size delta beyond which we deem the conversion
# to be a failure
set(MAX_DELTA 100000)

# Check md5sum for a match
if("${size_delta}" GREATER "${MAX_DELTA}")
  file(APPEND "${LOGFILE}" "\nFailure: ${dem_g_result}\ndem size delta is greater than ${MAX_DELTA} for ${CBDIR}/hawaii-c.g: ${size_delta}")
  message(FATAL_ERROR "[gcv] Failure while converting ${CBDIR}/hawaii-c, see ${LOGFILE} for more info.\n")
endif("${size_delta}" GREATER "${MAX_DELTA}")

# Cleanup
foreach(of ${OUTPUT_FILES})
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${of}")
endforeach(of ${OUTPUT_FILES})

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

