set(CBDIR "@CMAKE_CURRENT_BINARY_DIR@")
set(SRCIMG "@INPUT_IMAGE@")
set(CTRLIMG "@CONTROL_IMAGE@")
set(TGTIMG "@TARGET_IMAGE@")
set(LOGFILE "@LOG_FILE@")
set(TGT "@TARGET_NAME@")

set(SBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY@")
set(DBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG@")
set(RBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE@")
# Use the mechanism created to allow multiconfig builds to
# know what the current build type is.
if(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")
  set(BDIR "${RBDIR}")
elseif(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Debug")
  set(BDIR "${DBDIR}")
else(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")
  set(BDIR "${SBDIR}")
endif(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")

get_filename_component(EXE "@CMAKE_COMMAND@" EXT)
set(ICV "${BDIR}/icv${EXE}")
set(PIXCMP "${BDIR}/pixcmp${EXE}")

# Start with the file using standard UNIX line endings
file(APPEND "${LOGFILE}" "Converting ${SRCIMG} to ${TGTIMG}:\n")
execute_process(
  COMMAND "${ICV}" "${SRCIMG}" "${TGTIMG}"
  OUTPUT_VARIABLE icv_log ERROR_VARIABLE icv_log
  )
file(APPEND "${LOGFILE}" "${icv_log}")
set(icv_log)
if(NOT EXISTS "${TGTIMG}")
  message(FATAL_ERROR "Unable to convert ${SRCIMG} to ${TGTIMG} with ${ICV}, aborting.\nSee ${LOGFILE} for more details.")
endif(NOT EXISTS "${TGTIMG}")

# Because we are exporting, we need to convert both the control image
# and the target image to PIX format for pixcmp to do its check.
execute_process(
  COMMAND "${ICV}" "${CTRLIMG}" "${CMAKE_CURRENT_BINARY_DIR}/${TGT}-ctrl.pix"
  OUTPUT_VARIABLE icv_log ERROR_VARIABLE icv_log
  )
file(APPEND "${LOGFILE}" "${icv_log}")
set(icv_log)
execute_process(
  COMMAND "${ICV}" "${TGTIMG}" "${CMAKE_CURRENT_BINARY_DIR}/${TGT}-out.pix"
  OUTPUT_VARIABLE icv_log ERROR_VARIABLE icv_log
  )
file(APPEND "${LOGFILE}" "${icv_log}")
set(icv_log)

# pixcmp the results with the control image to make sure icv got it right
file(APPEND "${LOGFILE}" "\nComparing ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-ctrl.pix to ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-out.pix:\n")
execute_process(
  COMMAND "${PIXCMP}" "${CMAKE_CURRENT_BINARY_DIR}/${TGT}-ctrl.pix" "${CMAKE_CURRENT_BINARY_DIR}/${TGT}-out.pix"
  RESULT_VARIABLE pixcmp_val OUTPUT_VARIABLE icv_log ERROR_VARIABLE icv_log
  )
file(APPEND "${LOGFILE}" "${icv_log}")

if(${pixcmp_val} GREATER 0)
  message(FATAL_ERROR "Differences found between ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-ctrl.pix and ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-out.pix with ${PIXCMP}, aborting.\nSee ${LOGFILE} for more details.")
else(${pixcmp_val} GREATER 0)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove ${TGTIMG})
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-ctrl.pix)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove ${CMAKE_CURRENT_BINARY_DIR}/${TGT}-out.pix)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E touch "@STAMP_FILE@")
endif(${pixcmp_val} GREATER 0)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

