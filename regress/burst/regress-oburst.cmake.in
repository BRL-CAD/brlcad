set(CBDIR "@CMAKE_CURRENT_BINARY_DIR@")
set(CSDIR "@CMAKE_CURRENT_SOURCE_DIR@")
set(BURST_CF "@BURST_CLEAN_FILES@")
set(BURST_IF "@BURST_INPUT_FILES@")
set(BIN_DIR "@BIN_DIR@")
set(DDIR "@DATA_DIR@")
set(LOGFILE "${CBDIR}/regress-burst.log")

file(WRITE "${LOGFILE}" "Starting burst run\n")

# The executable locations aren't know at CMake configure time, so one of them
# is passed in via the EXEC variable at runtime.  De-quote it and assign it to
# the appropriate variable.
string(REPLACE "\\" "" BURST "${EXEC}")
if (NOT EXISTS "${BURST}")
  file(APPEND "${LOGFILE}" "burst not found at location \"${BURST}\" - aborting\n")
  message(FATAL_ERROR "Unable to find burst, aborting.\nSee ${LOGFILE} for more details.")
endif (NOT EXISTS "${BURST}")

# Clean up in case we've run before
foreach(BCF ${BURST_CF})
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${CBDIR}/${BCF}")
endforeach(BCF ${BURST_CF})

# Stage the input files, if we're not in the source directory
if (NOT "${CSDIR}" STREQUAL "${CBDIR}")
  foreach(BIF ${BURST_IF})
    execute_process(COMMAND "@CMAKE_COMMAND@" -E copy "${CSDIR}/${BIF}" "${CBDIR}/${BIF}")
  endforeach(BIF ${BURST_IF})
endif (NOT "${CSDIR}" STREQUAL "${CBDIR}")

# Find the root path from the executable, and from there the database path
get_filename_component(BDIR "${BURST}" DIRECTORY)
string(REGEX REPLACE "${BIN_DIR}$" "" RDIR "${BDIR}")
execute_process(COMMAND "@CMAKE_COMMAND@" -E copy "${RDIR}/${DDIR}/db/ktank.g" "${CBDIR}/ktank.g")

file(APPEND "${LOGFILE}" "Running burst on ktank.g:\n${BURST} -b ${CBDIR}/ktank.b\n")

execute_process(
  COMMAND "${BURST}" -b "${CBDIR}/ktank.b"
  RESULT_VARIABLE burst_result OUTPUT_VARIABLE burst_log ERROR_VARIABLE burst_log
  WORKING_DIRECTORY ${CBDIR}
  )
file(APPEND "${LOGFILE}" "${burst_log}")
set(burst_log)
if(NOT EXISTS "${CBDIR}/ktank.burst" OR burst_result)
  file(APPEND "${LOGFILE}" "Failure: ${burst_result}\n")
  file(READ "${LOGFILE}" LOG)
  message(FATAL_ERROR "${BURST} failed to process ktank.b, aborting.\nSee ${LOGFILE} for more details.\n${LOG}")
endif(NOT EXISTS "${CBDIR}/ktank.burst" OR burst_result)

# If we copied the input files, clear them out as well
if(NOT "${CSDIR}" STREQUAL "${CBDIR}")
  foreach(BIF ${BURST_INPUT_FILES})
    execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${CBDIR}/${BCF}")
  endforeach(BIF ${BURST_INPUT_FILES})
endif(NOT "${CSDIR}" STREQUAL "${CBDIR}")


# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

