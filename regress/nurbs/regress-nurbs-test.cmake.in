set(CBDIR "@CMAKE_CURRENT_BINARY_DIR@")
set(CSDIR "@CMAKE_CURRENT_SOURCE_DIR@")
set(DDIR "@DATA_DIR@")

# TODO - need to investigate if generator expressions might be used
# to fully replace this mechanism:
# https://cmake.org/cmake/help/latest/manual/cmake-generator-expressions.7.html
set(SBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY@")
set(DBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG@")
set(RBDIR "@CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE@")
# Use the mechanism created to allow multiconfig builds to
# know what the current build type is.
if(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")
  set(BDIR "${RBDIR}")
  set(test_suffix "-Release")
elseif(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Debug")
  set(BDIR "${DBDIR}")
  set(test_suffix "-Debug")
else(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")
  set(BDIR "${SBDIR}")
  set(test_suffix)
endif(EXISTS "@CMAKE_BINARY_DIR@/CMakeTmp/CURRENT_PATH/Release")

get_filename_component(EXE "@CMAKE_COMMAND@" EXT)
set(NHIT "${BDIR}/analyze_nhit${EXE}")

get_filename_component(GFILE_FP "@NHIT_GFILE@" ABSOLUTE)
if ("${GFILE_FP}" STREQUAL "@NHIT_GFILE@")
  set(NHIT_G "@NHIT_GFILE@")
else ("${GFILE_FP}" STREQUAL "@NHIT_GFILE@")
  set(NHIT_G "${BDIR}/../@NHIT_GFILE@")
endif ("${GFILE_FP}" STREQUAL "@NHIT_GFILE@")

execute_process(
  COMMAND "${NHIT}" @NHIT_OPTS@ "${NHIT_G}" @NHIT_ARGS@ RESULT_VARIABLE nurbs_nirt_result
  OUTPUT_VARIABLE nurbs_nirt_log ERROR_VARIABLE nurbs_nirt_log
  WORKING_DIRECTORY ${CBDIR}
  )

if(nurbs_nirt_result)
  message(FATAL_ERROR "[nurbs_nirt] Failure, unexpected result running ${NHIT} @NHIT_OPTS@ @NHIT_GFILE@ @NHIT_ARGS@\n ${nurbs_nirt_log}\n")
else(nurbs_nirt_result)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E touch "@CMAKE_CURRENT_BINARY_DIR@/@NHIT_NAME@${test_suffix}.done")
endif(nurbs_nirt_result)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

